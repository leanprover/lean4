/-
Copyright (c) 2014 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Jeremy Avigad, Leonardo de Moura
-/
import logic.connectives logic.identities algebra.binary
open eq.ops binary function

definition set (X : Type) := X â†’ Prop

namespace set

variable {X : Type}

/- membership and subset -/

definition mem (x : X) (a : set X) := a x
infix âˆˆ := mem
notation a âˆ‰ b := Â¬ mem a b

theorem ext {a b : set X} (H : âˆ€x, x âˆˆ a â†” x âˆˆ b) : a = b :=
funext (take x, propext (H x))

definition subset (a b : set X) := âˆ€â¦ƒxâ¦„, x âˆˆ a â†’ x âˆˆ b
infix âŠ† := subset

definition superset (s t : set X) : Prop := t âŠ† s
infix âŠ‡ := superset

theorem subset.refl (a : set X) : a âŠ† a := take x, assume H, H

theorem subset.trans {a b c : set X} (subab : a âŠ† b) (subbc : b âŠ† c) : a âŠ† c :=
take x, assume ax, subbc (subab ax)

theorem subset.antisymm {a b : set X} (hâ‚ : a âŠ† b) (hâ‚‚ : b âŠ† a) : a = b :=
ext (Î» x, iff.intro (Î» ina, hâ‚ ina) (Î» inb, hâ‚‚ inb))

-- an alterantive name
theorem eq_of_subset_of_subset {a b : set X} (hâ‚ : a âŠ† b) (hâ‚‚ : b âŠ† a) : a = b :=
subset.antisymm hâ‚ hâ‚‚

theorem mem_of_subset_of_mem {sâ‚ sâ‚‚ : set X} {a : X} : sâ‚ âŠ† sâ‚‚ â†’ a âˆˆ sâ‚ â†’ a âˆˆ sâ‚‚ :=
assume hâ‚ hâ‚‚, hâ‚ _ hâ‚‚

/- strict subset -/

definition strict_subset (a b : set X) := a âŠ† b âˆ§ a â‰  b
infix ` âŠ‚ `:50 := strict_subset

theorem strict_subset.irrefl (a : set X) : Â¬ a âŠ‚ a :=
assume h, absurd rfl (and.elim_right h)

/- bounded quantification -/

abbreviation bounded_forall (a : set X) (P : X â†’ Prop) := âˆ€â¦ƒxâ¦„, x âˆˆ a â†’ P x
notation `forallb` binders `âˆˆ` a `, ` r:(scoped:1 P, P) := bounded_forall a r
notation `âˆ€â‚€` binders `âˆˆ` a `, ` r:(scoped:1 P, P) := bounded_forall a r

abbreviation bounded_exists (a : set X) (P : X â†’ Prop) := âˆƒâ¦ƒxâ¦„, x âˆˆ a âˆ§ P x
notation `existsb` binders `âˆˆ` a `, ` r:(scoped:1 P, P) := bounded_exists a r
notation `âˆƒâ‚€` binders `âˆˆ` a `, ` r:(scoped:1 P, P) := bounded_exists a r

theorem bounded_exists.intro {P : X â†’ Prop} {s : set X} {x : X} (xs : x âˆˆ s) (Px : P x) :
  âˆƒâ‚€ x âˆˆ s, P x :=
exists.intro x (and.intro xs Px)

lemma bounded_forall_congr {A : Type} {S : set A} {P Q : A â†’ Prop} (H : âˆ€â‚€ xâˆˆS, P x â†” Q x) :
  (âˆ€â‚€ x âˆˆ S, P x) = (âˆ€â‚€ x âˆˆ S, Q x) :=
begin
  apply propext,
  apply forall_congr,
  intros x,
  apply imp_congr_right,
  apply H
end

lemma bounded_exists_congr {A : Type} {S : set A} {P Q : A â†’ Prop} (H : âˆ€â‚€ xâˆˆS, P x â†” Q x) :
  (âˆƒâ‚€ x âˆˆ S, P x) = (âˆƒâ‚€ x âˆˆ S, Q x) :=
begin
  apply propext,
  apply exists_congr,
  intros x,
  apply and_congr_right,
  apply H
end

section
  open classical

  lemma not_bounded_exists {A : Type} {S : set A} {P : A â†’ Prop} :
    (Â¬ (âˆƒâ‚€ x âˆˆ S, P x)) = (âˆ€â‚€ x âˆˆ S, Â¬ P x) :=
  begin
    rewrite forall_iff_not_exists,
    apply propext,
    apply forall_congr,
    intro x,
    rewrite not_and_iff_not_or_not,
    rewrite imp_iff_not_or
  end

  lemma not_bounded_forall {A : Type} {S : set A} {P : A â†’ Prop} :
    (Â¬ (âˆ€â‚€ x âˆˆ S, P x)) = (âˆƒâ‚€ x âˆˆ S, Â¬ P x) :=
  calc (Â¬ (âˆ€â‚€ x âˆˆ S, P x)) = Â¬ Â¬ (âˆƒâ‚€ x âˆˆ S, Â¬ P x) :
    begin
      rewrite not_bounded_exists,
      apply (congr_arg not),
      apply bounded_forall_congr,
      intros x H,
      rewrite not_not_iff
    end
    ... = (âˆƒâ‚€ x âˆˆ S, Â¬ P x) : by (rewrite not_not_iff)

end

/- empty set -/

definition empty : set X := Î»x, false
notation `âˆ…` := empty

theorem not_mem_empty (x : X) : Â¬ (x âˆˆ âˆ…) :=
assume H : x âˆˆ âˆ…, H

theorem mem_empty_eq (x : X) : x âˆˆ âˆ… = false := rfl

theorem eq_empty_of_forall_not_mem {s : set X} (H : âˆ€ x, x âˆ‰ s) : s = âˆ… :=
ext (take x, iff.intro
  (assume xs, absurd xs (H x))
  (assume xe, absurd xe !not_mem_empty))

theorem ne_empty_of_mem {s : set X} {x : X} (H : x âˆˆ s) : s â‰  âˆ… :=
  begin intro Hs, rewrite Hs at H, apply not_mem_empty _ H end

section
  open classical

  theorem exists_mem_of_ne_empty {s : set X} (H : s â‰  âˆ…) : âˆƒ x, x âˆˆ s :=
  by_contradiction (assume H', H (eq_empty_of_forall_not_mem (forall_not_of_not_exists H')))
end

theorem empty_subset (s : set X) : âˆ… âŠ† s :=
take x, assume H, false.elim H

theorem eq_empty_of_subset_empty {s : set X} (H : s âŠ† âˆ…) : s = âˆ… :=
subset.antisymm H (empty_subset s)

theorem subset_empty_iff (s : set X) : s âŠ† âˆ… â†” s = âˆ… :=
iff.intro eq_empty_of_subset_empty (take xeq, by rewrite xeq; apply subset.refl âˆ…)

lemma bounded_forall_empty_iff {P : X â†’ Prop} :
  (âˆ€â‚€xâˆˆâˆ…, P x) â†” true :=
iff.intro (take H, true.intro) (take H, by contradiction)

/- universal set -/

definition univ : set X := Î»x, true

theorem mem_univ (x : X) : x âˆˆ univ := trivial

theorem mem_univ_iff (x : X) : x âˆˆ univ â†” true := !iff.refl

theorem mem_univ_eq (x : X) : x âˆˆ univ = true := rfl

theorem empty_ne_univ [h : inhabited X] : (empty : set X) â‰  univ :=
assume H : empty = univ,
absurd (mem_univ (inhabited.value h)) (eq.rec_on H (not_mem_empty _))

theorem subset_univ (s : set X) : s âŠ† univ := Î» x H, trivial

theorem eq_univ_of_univ_subset {s : set X} (H : univ âŠ† s) : s = univ :=
eq_of_subset_of_subset (subset_univ s) H

theorem eq_univ_of_forall {s : set X} (H : âˆ€ x, x âˆˆ s) : s = univ :=
ext (take x, iff.intro (assume H', trivial) (assume H', H x))

/- union -/

definition union (a b : set X) : set X := Î»x, x âˆˆ a âˆ¨ x âˆˆ b
notation a âˆª b := union a b

theorem mem_union_left {x : X} {a : set X} (b : set X) : x âˆˆ a â†’ x âˆˆ a âˆª b :=
assume h, or.inl h

theorem mem_union_right {x : X} {b : set X} (a : set X) : x âˆˆ b â†’ x âˆˆ a âˆª b :=
assume h, or.inr h

theorem mem_unionl {x : X} {a b : set X} : x âˆˆ a â†’ x âˆˆ a âˆª b :=
assume h, or.inl h

theorem mem_unionr {x : X} {a b : set X} : x âˆˆ b â†’ x âˆˆ a âˆª b :=
assume h, or.inr h

theorem mem_or_mem_of_mem_union {x : X} {a b : set X} (H : x âˆˆ a âˆª b) : x âˆˆ a âˆ¨ x âˆˆ b := H

theorem mem_union.elim {x : X} {a b : set X} {P : Prop}
    (Hâ‚ : x âˆˆ a âˆª b) (Hâ‚‚ : x âˆˆ a â†’ P) (Hâ‚ƒ : x âˆˆ b â†’ P) : P :=
or.elim Hâ‚ Hâ‚‚ Hâ‚ƒ

theorem mem_union_iff (x : X) (a b : set X) : x âˆˆ a âˆª b â†” x âˆˆ a âˆ¨ x âˆˆ b := !iff.refl

theorem mem_union_eq (x : X) (a b : set X) : x âˆˆ a âˆª b = (x âˆˆ a âˆ¨ x âˆˆ b) := rfl

theorem union_self (a : set X) : a âˆª a = a :=
ext (take x, !or_self)

theorem union_empty (a : set X) : a âˆª âˆ… = a :=
ext (take x, !or_false)

theorem empty_union (a : set X) : âˆ… âˆª a = a :=
ext (take x, !false_or)

theorem union_comm (a b : set X) : a âˆª b = b âˆª a :=
ext (take x, or.comm)

theorem union_assoc (a b c : set X) : (a âˆª b) âˆª c = a âˆª (b âˆª c) :=
ext (take x, or.assoc)

theorem union_left_comm (sâ‚ sâ‚‚ sâ‚ƒ : set X) : sâ‚ âˆª (sâ‚‚ âˆª sâ‚ƒ) = sâ‚‚ âˆª (sâ‚ âˆª sâ‚ƒ) :=
!left_comm union_comm union_assoc sâ‚ sâ‚‚ sâ‚ƒ

theorem union_right_comm (sâ‚ sâ‚‚ sâ‚ƒ : set X) : (sâ‚ âˆª sâ‚‚) âˆª sâ‚ƒ = (sâ‚ âˆª sâ‚ƒ) âˆª sâ‚‚ :=
!right_comm union_comm union_assoc sâ‚ sâ‚‚ sâ‚ƒ

theorem subset_union_left (s t : set X) : s âŠ† s âˆª t := Î» x H, or.inl H

theorem subset_union_right (s t : set X) : t âŠ† s âˆª t := Î» x H, or.inr H

theorem union_subset {s t r : set X} (sr : s âŠ† r) (tr : t âŠ† r) : s âˆª t âŠ† r :=
Î» x xst, or.elim xst (Î» xs, sr xs) (Î» xt, tr xt)

/- intersection -/

definition inter (a b : set X) : set X := Î»x, x âˆˆ a âˆ§ x âˆˆ b
notation a âˆ© b := inter a b

theorem mem_inter_iff (x : X) (a b : set X) : x âˆˆ a âˆ© b â†” x âˆˆ a âˆ§ x âˆˆ b := !iff.refl

theorem mem_inter_eq (x : X) (a b : set X) : x âˆˆ a âˆ© b = (x âˆˆ a âˆ§ x âˆˆ b) := rfl

theorem mem_inter {x : X} {a b : set X} (Ha : x âˆˆ a) (Hb : x âˆˆ b) : x âˆˆ a âˆ© b :=
and.intro Ha Hb

theorem mem_of_mem_inter_left {x : X} {a b : set X} (H : x âˆˆ a âˆ© b) : x âˆˆ a :=
and.left H

theorem mem_of_mem_inter_right {x : X} {a b : set X} (H : x âˆˆ a âˆ© b) : x âˆˆ b :=
and.right H

theorem inter_self (a : set X) : a âˆ© a = a :=
ext (take x, !and_self)

theorem inter_empty (a : set X) : a âˆ© âˆ… = âˆ… :=
ext (take x, !and_false)

theorem empty_inter (a : set X) : âˆ… âˆ© a = âˆ… :=
ext (take x, !false_and)

theorem nonempty_of_inter_nonempty_right {T : Type} {s t : set T} (H : s âˆ© t â‰  âˆ…) : t â‰  âˆ… :=
suppose t = âˆ…,
have s âˆ© t = âˆ…, by rewrite this; apply inter_empty,
H this

theorem nonempty_of_inter_nonempty_left {T : Type} {s t : set T} (H : s âˆ© t â‰  âˆ…) : s â‰  âˆ… :=
suppose s = âˆ…,
have s âˆ© t = âˆ…, by rewrite this; apply empty_inter,
H this

theorem inter_comm (a b : set X) : a âˆ© b = b âˆ© a :=
ext (take x, !and.comm)

theorem inter_assoc (a b c : set X) : (a âˆ© b) âˆ© c = a âˆ© (b âˆ© c) :=
ext (take x, !and.assoc)

theorem inter_left_comm (sâ‚ sâ‚‚ sâ‚ƒ : set X) : sâ‚ âˆ© (sâ‚‚ âˆ© sâ‚ƒ) = sâ‚‚ âˆ© (sâ‚ âˆ© sâ‚ƒ) :=
!left_comm inter_comm inter_assoc sâ‚ sâ‚‚ sâ‚ƒ

theorem inter_right_comm (sâ‚ sâ‚‚ sâ‚ƒ : set X) : (sâ‚ âˆ© sâ‚‚) âˆ© sâ‚ƒ = (sâ‚ âˆ© sâ‚ƒ) âˆ© sâ‚‚ :=
!right_comm inter_comm inter_assoc sâ‚ sâ‚‚ sâ‚ƒ

theorem inter_univ (a : set X) : a âˆ© univ = a :=
ext (take x, !and_true)

theorem univ_inter (a : set X) : univ âˆ© a = a :=
ext (take x, !true_and)

theorem inter_subset_left (s t : set X) : s âˆ© t âŠ† s := Î» x H, and.left H

theorem inter_subset_right (s t : set X) : s âˆ© t âŠ† t := Î» x H, and.right H

theorem inter_subset_inter_right {s t : set X} (u : set X) (H : s âŠ† t) : s âˆ© u âŠ† t âˆ© u :=
take x, assume xsu, and.intro (H (and.left xsu)) (and.right xsu)

theorem inter_subset_inter_left {s t : set X} (u : set X) (H : s âŠ† t) : u âˆ© s âŠ† u âˆ© t :=
take x, assume xus, and.intro (and.left xus) (H (and.right xus))

theorem subset_inter {s t r : set X} (rs : r âŠ† s) (rt : r âŠ† t) : r âŠ† s âˆ© t :=
Î» x xr, and.intro (rs xr) (rt xr)

theorem not_mem_of_mem_of_not_mem_inter_left {s t : set X} {x : X} (Hxs : x âˆˆ s) (Hnm : x âˆ‰ s âˆ© t) : x âˆ‰ t :=
  suppose x âˆˆ t,
  have x âˆˆ s âˆ© t, from and.intro Hxs this,
  show false, from Hnm this

theorem not_mem_of_mem_of_not_mem_inter_right {s t : set X} {x : X} (Hxs : x âˆˆ t) (Hnm : x âˆ‰ s âˆ© t) : x âˆ‰ s :=
  suppose x âˆˆ s,
  have x âˆˆ s âˆ© t, from and.intro this Hxs,
  show false, from Hnm this

/- distributivity laws -/

theorem inter_distrib_left (s t u : set X) : s âˆ© (t âˆª u) = (s âˆ© t) âˆª (s âˆ© u) :=
ext (take x, !and.left_distrib)

theorem inter_distrib_right (s t u : set X) : (s âˆª t) âˆ© u = (s âˆ© u) âˆª (t âˆ© u) :=
ext (take x, !and.right_distrib)

theorem union_distrib_left (s t u : set X) : s âˆª (t âˆ© u) = (s âˆª t) âˆ© (s âˆª u) :=
ext (take x, !or.left_distrib)

theorem union_distrib_right (s t u : set X) : (s âˆ© t) âˆª u = (s âˆª u) âˆ© (t âˆª u) :=
ext (take x, !or.right_distrib)

/- set-builder notation -/

-- {x : X | P}
definition set_of (P : X â†’ Prop) : set X := P
notation `{` binder ` | ` r:(scoped:1 P, set_of P) `}` := r

-- {x âˆˆ s | P}
definition sep (P : X â†’ Prop) (s : set X) : set X := Î»x, x âˆˆ s âˆ§ P x
notation `{` binder ` âˆˆ ` s ` | ` r:(scoped:1 p, sep p s) `}` := r

/- insert -/

definition insert (x : X) (a : set X) : set X := {y : X | y = x âˆ¨ y âˆˆ a}

-- '{x, y, z}
notation `'{`:max a:(foldr `, ` (x b, insert x b) âˆ…) `}`:0 := a

theorem subset_insert (x : X) (a : set X) : a âŠ† insert x a :=
take y, assume ys, or.inr ys

theorem mem_insert (x : X) (s : set X) : x âˆˆ insert x s :=
or.inl rfl

theorem mem_insert_of_mem {x : X} {s : set X} (y : X) : x âˆˆ s â†’ x âˆˆ insert y s :=
assume h, or.inr h

theorem eq_or_mem_of_mem_insert {x a : X} {s : set X} : x âˆˆ insert a s â†’ x = a âˆ¨ x âˆˆ s :=
assume h, h

theorem mem_of_mem_insert_of_ne {x a : X} {s : set X} (xin : x âˆˆ insert a s) : x â‰  a â†’ x âˆˆ s :=
or_resolve_right (eq_or_mem_of_mem_insert xin)

theorem mem_insert_eq (x a : X) (s : set X) : x âˆˆ insert a s = (x = a âˆ¨ x âˆˆ s) :=
propext (iff.intro !eq_or_mem_of_mem_insert
  (or.rec (Î»H', (eq.substr H' !mem_insert)) !mem_insert_of_mem))

theorem insert_eq_of_mem {a : X} {s : set X} (H : a âˆˆ s) : insert a s = s :=
ext (Î» x, eq.substr (mem_insert_eq x a s)
   (or_iff_right_of_imp (Î»H1, eq.substr H1 H)))

theorem insert.comm (x y : X) (s : set X) : insert x (insert y s) = insert y (insert x s) :=
ext (take a, by rewrite [*mem_insert_eq, propext !or.left_comm])

-- useful in proofs by induction
theorem forall_of_forall_insert {P : X â†’ Prop} {a : X} {s : set X}
    (H : âˆ€ x, x âˆˆ insert a s â†’ P x) :
  âˆ€ x, x âˆˆ s â†’ P x :=
Î» x xs, H x (!mem_insert_of_mem xs)

lemma bounded_forall_insert_iff {P : X â†’ Prop} {a : X} {s : set X} :
  (âˆ€â‚€x âˆˆ insert a s, P x) â†” P a âˆ§ (âˆ€â‚€x âˆˆ s, P x) :=
begin
  apply iff.intro, all_goals (intro H),
  { apply and.intro,
    { apply H, apply mem_insert },
    { intro x Hx, apply H, apply mem_insert_of_mem, assumption } },
  { intro x Hx, cases Hx with eq Hx,
    { cases eq, apply (and.elim_left H) },
    { apply (and.elim_right H), assumption } }
end

/- singleton -/

theorem mem_singleton_iff (a b : X) : a âˆˆ '{b} â†” a = b :=
iff.intro
  (assume ainb, or.elim ainb (Î» aeqb, aeqb) (Î» f, false.elim f))
  (assume aeqb, or.inl aeqb)

theorem mem_singleton (a : X) : a âˆˆ '{a} := !mem_insert

theorem eq_of_mem_singleton {x y : X} (h : x âˆˆ '{y}) : x = y :=
or.elim (eq_or_mem_of_mem_insert h)
  (suppose x = y, this)
  (suppose x âˆˆ âˆ…, absurd this !not_mem_empty)

theorem mem_singleton_of_eq {x y : X} (H : x = y) : x âˆˆ '{y} :=
eq.symm H â–¸ mem_singleton y

theorem insert_eq (x : X) (s : set X) : insert x s = '{x} âˆª s :=
ext (take y, iff.intro
  (suppose y âˆˆ insert x s,
    or.elim this (suppose y = x, or.inl (or.inl this)) (suppose y âˆˆ s, or.inr this))
  (suppose y âˆˆ '{x} âˆª s,
    or.elim this
      (suppose y âˆˆ '{x}, or.inl (eq_of_mem_singleton this))
      (suppose y âˆˆ s, or.inr this)))

theorem pair_eq_singleton (a : X) : '{a, a} = '{a} :=
by rewrite [insert_eq_of_mem !mem_singleton]

theorem singleton_ne_empty (a : X) : '{a} â‰  âˆ… :=
begin
  intro H,
  apply not_mem_empty a,
  rewrite -H,
  apply mem_insert
end

/- separation -/

theorem mem_sep {s : set X} {P : X â†’ Prop} {x : X} (xs : x âˆˆ s) (Px : P x) : x âˆˆ {x âˆˆ s | P x} :=
and.intro xs Px

theorem eq_sep_of_subset {s t : set X} (ssubt : s âŠ† t) : s = {x âˆˆ t | x âˆˆ s} :=
ext (take x, iff.intro
  (suppose x âˆˆ s, and.intro (ssubt this) this)
  (suppose x âˆˆ {x âˆˆ t | x âˆˆ s}, and.right this))

theorem mem_sep_iff {s : set X} {P : X â†’ Prop} {x : X} : x âˆˆ {x âˆˆ s | P x} â†” x âˆˆ s âˆ§ P x :=
!iff.refl

theorem sep_subset (s : set X) (P : X â†’ Prop) : {x âˆˆ s | P x} âŠ† s :=
take x, assume H, and.left H

theorem forall_not_of_sep_empty {s : set X} {P : X â†’ Prop} (H : {x âˆˆ s | P x} = âˆ…) : âˆ€â‚€ x âˆˆ s, Â¬ P x :=
  take x, suppose x âˆˆ s, suppose P x,
  have x âˆˆ {x âˆˆ s | P x}, from and.intro `x âˆˆ s` this,
  show false, from ne_empty_of_mem this H

/- complement -/

definition compl (s : set X) : set X := {x | x âˆ‰ s}
prefix `-` := compl

theorem mem_compl {s : set X} {x : X} (H : x âˆ‰ s) : x âˆˆ -s := H

theorem not_mem_of_mem_compl {s : set X} {x : X} (H : x âˆˆ -s) : x âˆ‰ s := H

theorem mem_compl_iff (s : set X) (x : X) : x âˆˆ -s â†” x âˆ‰ s := !iff.refl

theorem inter_compl_self (s : set X) : s âˆ© -s = âˆ… :=
ext (take x, !and_not_self_iff)

theorem compl_inter_self (s : set X) : -s âˆ© s = âˆ… :=
ext (take x, !not_and_self_iff)

/- some classical identities -/

section
  open classical

  theorem compl_empty : -(âˆ… : set X) = univ :=
  ext (take x, iff.intro (assume H, trivial) (assume H, not_false))

  theorem compl_union (s t : set X) : -(s âˆª t) = -s âˆ© -t :=
  ext (take x, !not_or_iff_not_and_not)

  theorem compl_compl (s : set X) : -(-s) = s :=
  ext (take x, !not_not_iff)

  theorem compl_inter (s t : set X) : -(s âˆ© t) = -s âˆª -t :=
  ext (take x, !not_and_iff_not_or_not)

  theorem compl_univ : -(univ : set X) = âˆ… :=
  by rewrite [-compl_empty, compl_compl]

  theorem union_eq_compl_compl_inter_compl (s t : set X) : s âˆª t = -(-s âˆ© -t) :=
  ext (take x, !or_iff_not_and_not)

  theorem inter_eq_compl_compl_union_compl (s t : set X) : s âˆ© t = -(-s âˆª -t) :=
  ext (take x, !and_iff_not_or_not)

  theorem union_compl_self (s : set X) : s âˆª -s = univ :=
  ext (take x, !or_not_self_iff)

  theorem compl_union_self (s : set X) : -s âˆª s = univ :=
  ext (take x, !not_or_self_iff)

  theorem compl_comp_compl :
    #function compl âˆ˜ compl = @id (set X) :=
  funext (Î» s, compl_compl s)
end

/- set difference -/

definition diff (s t : set X) : set X := {x âˆˆ s | x âˆ‰ t}
infix `\`:70 := diff

theorem mem_diff {s t : set X} {x : X} (H1 : x âˆˆ s) (H2 : x âˆ‰ t) : x âˆˆ s \ t :=
and.intro H1 H2

theorem mem_of_mem_diff {s t : set X} {x : X} (H : x âˆˆ s \ t) : x âˆˆ s :=
and.left H

theorem not_mem_of_mem_diff {s t : set X} {x : X} (H : x âˆˆ s \ t) : x âˆ‰ t :=
and.right H

theorem mem_diff_iff (s t : set X) (x : X) : x âˆˆ s \ t â†” x âˆˆ s âˆ§ x âˆ‰ t := !iff.refl

theorem mem_diff_eq (s t : set X) (x : X) : x âˆˆ s \ t = (x âˆˆ s âˆ§ x âˆ‰ t) := rfl

theorem diff_eq (s t : set X) : s \ t = s âˆ© -t := rfl

theorem union_diff_cancel {s t : set X} [dec : Î  x, decidable (x âˆˆ s)] (H : s âŠ† t) : s âˆª (t \ s) = t :=
ext (take x, iff.intro
  (assume H1 : x âˆˆ s âˆª (t \ s), or.elim H1 (assume H2, !H H2) (assume H2, and.left H2))
  (assume H1 : x âˆˆ t,
    decidable.by_cases
      (suppose x âˆˆ s, or.inl this)
      (suppose x âˆ‰ s, or.inr (and.intro H1 this))))

theorem diff_subset (s t : set X) : s \ t âŠ† s := inter_subset_left s _

theorem compl_eq_univ_diff (s : set X) : -s = univ \ s :=
ext (take x, iff.intro (assume H, and.intro trivial H) (assume H, and.right H))

/- powerset -/

definition powerset (s : set X) : set (set X) := {x : set X | x âŠ† s}
prefix `ğ’«`:100 := powerset

theorem mem_powerset {x s : set X} (H : x âŠ† s) : x âˆˆ ğ’« s := H

theorem subset_of_mem_powerset {x s : set X} (H : x âˆˆ ğ’« s) : x âŠ† s := H

theorem mem_powerset_iff (x s : set X) : x âˆˆ ğ’« s â†” x âŠ† s := !iff.refl

/- function image -/

section image

variables {Y Z : Type}

abbreviation eq_on (f1 f2 : X â†’ Y) (a : set X) : Prop :=
âˆ€â‚€ x âˆˆ a, f1 x = f2 x

definition image (f : X â†’ Y) (a : set X) : set Y := {y : Y | âˆƒx, x âˆˆ a âˆ§ f x = y}
infix ` ' ` := image

theorem image_eq_image_of_eq_on {f1 f2 : X â†’ Y} {a : set X} (H1 : eq_on f1 f2 a) :
  f1 ' a = f2 ' a :=
ext (take y, iff.intro
  (assume H2,
    obtain x (H3 : x âˆˆ a âˆ§ f1 x = y), from H2,
    have H4 : x âˆˆ a, from and.left H3,
    have H5 : f2 x = y, from (H1 H4)â»Â¹ â¬ and.right H3,
    exists.intro x (and.intro H4 H5))
  (assume H2,
    obtain x (H3 : x âˆˆ a âˆ§ f2 x = y), from H2,
    have H4 : x âˆˆ a, from and.left H3,
    have H5 : f1 x = y, from (H1 H4) â¬ and.right H3,
    exists.intro x (and.intro H4 H5)))

theorem mem_image {f : X â†’ Y} {a : set X} {x : X} {y : Y}
  (H1 : x âˆˆ a) (H2 : f x = y) : y âˆˆ f ' a :=
exists.intro x (and.intro H1 H2)

theorem mem_image_of_mem (f : X â†’ Y) {x : X} {a : set X} (H : x âˆˆ a) : f x âˆˆ image f a :=
mem_image H rfl

lemma image_comp (f : Y â†’ Z) (g : X â†’ Y) (a : set X) : (f âˆ˜ g) ' a = f ' (g ' a) :=
ext (take z,
  iff.intro
    (assume Hz : z âˆˆ (f âˆ˜ g) ' a,
      obtain x (Hxâ‚ : x âˆˆ a) (Hxâ‚‚ : f (g x) = z), from Hz,
      have Hgx : g x âˆˆ g ' a, from mem_image Hxâ‚ rfl,
      show z âˆˆ f ' (g ' a), from mem_image Hgx Hxâ‚‚)
    (assume Hz : z âˆˆ f ' (g 'a),
      obtain y (Hyâ‚ : y âˆˆ g ' a) (Hyâ‚‚ : f y = z), from Hz,
      obtain x (Hzâ‚ : x âˆˆ a) (Hzâ‚‚ : g x = y),      from Hyâ‚,
      show z âˆˆ (f âˆ˜ g) ' a, from mem_image Hzâ‚ (Hzâ‚‚â»Â¹ â–¸ Hyâ‚‚)))

lemma image_subset {a b : set X} (f : X â†’ Y) (H : a âŠ† b) : f ' a âŠ† f ' b :=
take y, assume Hy : y âˆˆ f ' a,
obtain x (Hxâ‚ : x âˆˆ a) (Hxâ‚‚ : f x = y), from Hy,
mem_image (H Hxâ‚) Hxâ‚‚

theorem image_union (f : X â†’ Y) (s t : set X) :
  image f (s âˆª t) = image f s âˆª image f t :=
ext (take y, iff.intro
  (assume H : y âˆˆ image f (s âˆª t),
    obtain x [(xst : x âˆˆ s âˆª t) (fxy : f x = y)], from H,
    or.elim xst
      (assume xs, or.inl (mem_image xs fxy))
      (assume xt, or.inr (mem_image xt fxy)))
  (assume H : y âˆˆ image f s âˆª image f t,
    or.elim H
      (assume yifs : y âˆˆ image f s,
        obtain x [(xs : x âˆˆ s) (fxy : f x = y)], from yifs,
        mem_image (or.inl xs) fxy)
      (assume yift : y âˆˆ image f t,
        obtain x [(xt : x âˆˆ t) (fxy : f x = y)], from yift,
        mem_image (or.inr xt) fxy)))

theorem image_empty (f : X â†’ Y) : image f âˆ… = âˆ… :=
eq_empty_of_forall_not_mem
  (take y, suppose y âˆˆ image f âˆ…,
    obtain x [(H : x âˆˆ empty) H'], from this,
    H)

theorem mem_image_compl (t : set X) (S : set (set X)) :
  t âˆˆ compl ' S â†” -t âˆˆ S :=
iff.intro
  (suppose t âˆˆ compl ' S,
    obtain t' [(Ht' : t' âˆˆ S) (Ht : -t' = t)], from this,
    show -t âˆˆ S, by rewrite [-Ht, compl_compl]; exact Ht')
  (suppose -t âˆˆ S,
    have -(-t) âˆˆ compl 'S, from mem_image_of_mem compl this,
    show t âˆˆ compl 'S, from compl_compl t â–¸ this)

theorem image_id (s : set X) : id ' s = s :=
ext (take x, iff.intro
  (suppose x âˆˆ id ' s,
    obtain x' [(Hx' : x' âˆˆ s) (x'eq : x' = x)], from this,
    show x âˆˆ s, by rewrite [-x'eq]; apply Hx')
  (suppose x âˆˆ s, mem_image_of_mem id this))

theorem compl_compl_image (S : set (set X)) :
  compl ' (compl ' S) = S :=
by rewrite [-image_comp, compl_comp_compl, image_id]

lemma bounded_forall_image_of_bounded_forall {f : X â†’ Y} {S : set X} {P : Y â†’ Prop}
  (H : âˆ€â‚€ x âˆˆ S, P (f x)) : âˆ€â‚€ y âˆˆ f ' S, P y :=
begin
  intro x' Hx;
  cases Hx with x Hx;
  cases Hx with Hx eq;
  rewrite (eqâ»Â¹);
  apply H;
  assumption
end

lemma bounded_forall_image_iff {f : X â†’ Y} {S : set X} {P : Y â†’ Prop} :
  (âˆ€â‚€ y âˆˆ f ' S, P y) â†” (âˆ€â‚€ x âˆˆ S, P (f x)) :=
iff.intro (take H x Hx, H _ (!mem_image_of_mem `x âˆˆ S`)) bounded_forall_image_of_bounded_forall

lemma image_insert_eq {f : X â†’ Y} {a : X} {S : set X} :
  f ' insert a S = insert (f a) (f ' S) :=
begin
  apply set.ext,
  intro x, apply iff.intro, all_goals (intros H),
  { cases H with y Hy, cases Hy with Hy eq, rewrite (eqâ»Â¹), cases Hy with y_eq,
    { rewrite y_eq, apply mem_insert },
    { apply mem_insert_of_mem, apply mem_image_of_mem, assumption } },
  { cases H with eq Hx,
    { rewrite eq, apply mem_image_of_mem, apply mem_insert },
    { cases Hx with y Hy, cases Hy with Hy eq,
      rewrite (eqâ»Â¹), apply mem_image_of_mem, apply mem_insert_of_mem, assumption } }
end

end image

/- collections of disjoint sets -/

definition disjoint_sets (S : set (set X)) : Prop := âˆ€ a b, a âˆˆ S â†’ b âˆˆ S â†’ a â‰  b â†’ a âˆ© b = âˆ…

theorem disjoint_sets_empty : disjoint_sets (âˆ… : set (set X)) :=
take a b, assume H, !not.elim !not_mem_empty H

theorem disjoint_sets_union {s t : set (set X)} (Hs : disjoint_sets s) (Ht : disjoint_sets t)
    (H : âˆ€ x y, x âˆˆ s âˆ§ y âˆˆ t â†’ x âˆ© y = âˆ…) :
  disjoint_sets (s âˆª t) :=
take a b, assume Ha Hb Hneq, or.elim Ha
 (assume H1, or.elim Hb
   (suppose b âˆˆ s, (Hs a b) H1 this Hneq)
   (suppose b âˆˆ t, (H a b) (and.intro H1 this)))
 (assume H2, or.elim Hb
   (suppose b âˆˆ s, !inter_comm â–¸ ((H b a) (and.intro this H2)))
   (suppose b âˆˆ t, (Ht a b) H2 this Hneq))

theorem disjoint_sets_singleton (s : set (set X)) : disjoint_sets '{s} :=
take a b, assume Ha Hb  Hneq,
absurd (eq.trans ((iff.elim_left !mem_singleton_iff) Ha) ((iff.elim_left !mem_singleton_iff) Hb)â»Â¹)
    Hneq

/- large unions -/

section large_unions
  variables {I : Type}
  variable a : set I
  variable b : I â†’ set X
  variable C : set (set X)

  definition sUnion : set X := {x : X | âˆƒâ‚€ c âˆˆ C, x âˆˆ c}
  definition sInter : set X := {x : X | âˆ€â‚€ c âˆˆ C, x âˆˆ c}

  prefix `â‹ƒâ‚€`:110 := sUnion
  prefix `â‹‚â‚€`:110 := sInter

  definition Union  : set X := {x : X | âˆƒi, x âˆˆ b i}
  definition Inter  : set X := {x : X | âˆ€i, x âˆˆ b i}

  notation `â‹ƒ` binders `, ` r:(scoped f, Union f) := r
  notation `â‹‚` binders `, ` r:(scoped f, Inter f) := r

  definition bUnion : set X := {x : X | âˆƒâ‚€ i âˆˆ a, x âˆˆ b i}
  definition bInter : set X := {x : X | âˆ€â‚€ i âˆˆ a, x âˆˆ b i}

  notation `â‹ƒ` binders ` âˆˆ ` s `, ` r:(scoped f, bUnion s f) := r
  notation `â‹‚` binders ` âˆˆ ` s `, ` r:(scoped f, bInter s f) := r

end large_unions

-- sUnion and sInter: a collection (set) of sets

theorem mem_sUnion {x : X} {t : set X} {S : set (set X)} (Hx : x âˆˆ t) (Ht : t âˆˆ S) :
  x âˆˆ â‹ƒâ‚€ S :=
exists.intro t (and.intro Ht Hx)

theorem not_mem_of_not_mem_sUnion {x : X} {t : set X} {S : set (set X)} (Hx : x âˆ‰ â‹ƒâ‚€ S) (Ht : t âˆˆ S) :
        x âˆ‰ t :=
  suppose x âˆˆ t,
  have x âˆˆ â‹ƒâ‚€ S, from mem_sUnion this Ht,
  show false, from Hx this

theorem mem_sInter {x : X} {t : set X} {S : set (set X)} (H : âˆ€â‚€ t âˆˆ S, x âˆˆ t) :
  x âˆˆ â‹‚â‚€ S :=
H

theorem sInter_subset_of_mem {S : set (set X)} {t : set X} (tS : t âˆˆ S) :
  (â‹‚â‚€ S) âŠ† t :=
take x, assume H, H t tS

theorem subset_sUnion_of_mem {S : set (set X)} {t : set X} (tS : t âˆˆ S) :
  t âŠ† (â‹ƒâ‚€ S) :=
take x, assume H, exists.intro t (and.intro tS H)

theorem sUnion_empty : â‹ƒâ‚€ âˆ… = (âˆ… : set X) :=
eq_empty_of_forall_not_mem
  (take x, suppose x âˆˆ sUnion âˆ…,
    obtain t [(Ht : t âˆˆ âˆ…) Ht'], from this,
    show false, from Ht)

theorem sInter_empty : â‹‚â‚€ âˆ… = (univ : set X) :=
eq_univ_of_forall (Î» x s H, false.elim H)

theorem sUnion_singleton (s : set X) : â‹ƒâ‚€ '{s} = s :=
ext (take x, iff.intro
  (suppose x âˆˆ sUnion '{s},
    obtain u [(Hu : u âˆˆ '{s}) (xu : x âˆˆ u)], from this,
    have u = s, from eq_of_mem_singleton Hu,
    show x âˆˆ s, using this, by rewrite -this; apply xu)
  (suppose x âˆˆ s,
    mem_sUnion this (mem_singleton s)))

theorem sInter_singleton (s : set X) : â‹‚â‚€ '{s} = s :=
ext (take x, iff.intro
  (suppose x âˆˆ â‹‚â‚€ '{s}, show x âˆˆ s, from this (mem_singleton s))
  (suppose x âˆˆ s, take u, suppose u âˆˆ '{s},
    show x âˆˆ u, by+ rewrite [eq_of_mem_singleton this]; assumption))

theorem sUnion_union (S T : set (set X)) : â‹ƒâ‚€ (S âˆª T) = â‹ƒâ‚€ S âˆª â‹ƒâ‚€ T :=
ext (take x, iff.intro
  (suppose x âˆˆ sUnion (S âˆª T),
    obtain u [(Hu : u âˆˆ S âˆª T) (xu : x âˆˆ u)], from this,
    or.elim Hu
      (assume uS, or.inl (mem_sUnion xu uS))
      (assume uT, or.inr (mem_sUnion xu uT)))
  (suppose x âˆˆ sUnion S âˆª sUnion T,
    or.elim this
      (suppose x âˆˆ sUnion S,
        obtain u [(uS : u âˆˆ S) (xu : x âˆˆ u)], from this,
        mem_sUnion xu (or.inl uS))
      (suppose x âˆˆ sUnion T,
        obtain u [(uT : u âˆˆ T) (xu : x âˆˆ u)], from this,
        mem_sUnion xu (or.inr uT))))

theorem sInter_union (S T : set (set X)) : â‹‚â‚€ (S âˆª T) = â‹‚â‚€ S âˆ© â‹‚â‚€ T :=
ext (take x, iff.intro
  (assume H : x âˆˆ â‹‚â‚€ (S âˆª T),
    and.intro (Î» u uS, H (or.inl uS)) (Î» u uT, H (or.inr uT)))
  (assume H : x âˆˆ â‹‚â‚€ S âˆ© â‹‚â‚€ T,
    take u, suppose u âˆˆ S âˆª T, or.elim this (Î» uS, and.left H u uS) (Î» uT, and.right H u uT)))

theorem sUnion_insert (s : set X) (T : set (set X)) :
  â‹ƒâ‚€ (insert s T) = s âˆª â‹ƒâ‚€ T :=
by rewrite [insert_eq, sUnion_union, sUnion_singleton]

theorem sInter_insert (s : set X) (T : set (set X)) :
  â‹‚â‚€ (insert s T) = s âˆ© â‹‚â‚€ T :=
by rewrite [insert_eq, sInter_union, sInter_singleton]

theorem compl_sUnion (S : set (set X)) :
  - â‹ƒâ‚€ S = â‹‚â‚€ (compl ' S) :=
ext (take x, iff.intro
  (assume H : x âˆˆ -(â‹ƒâ‚€ S),
    take t, suppose t âˆˆ compl ' S,
    obtain t' [(Ht' : t' âˆˆ S) (Ht : -t' = t)], from this,
    have x âˆˆ -t', from suppose x âˆˆ t', H (mem_sUnion this Ht'),
    show x âˆˆ t, using this, by rewrite -Ht; apply this)
  (assume H : x âˆˆ â‹‚â‚€ (compl ' S),
    suppose x âˆˆ â‹ƒâ‚€ S,
    obtain t [(tS : t âˆˆ S) (xt : x âˆˆ t)], from this,
    have -t âˆˆ compl ' S, from mem_image_of_mem compl tS,
    have x âˆˆ -t, from H this,
    show false, proof this xt qed))

theorem sUnion_eq_compl_sInter_compl (S : set (set X)) :
  â‹ƒâ‚€ S = - â‹‚â‚€ (compl ' S) :=
by rewrite [-compl_compl, compl_sUnion]

theorem compl_sInter (S : set (set X)) :
  - â‹‚â‚€ S = â‹ƒâ‚€ (compl ' S) :=
by rewrite [sUnion_eq_compl_sInter_compl, compl_compl_image]

theorem sInter_eq_comp_sUnion_compl (S : set (set X)) :
   â‹‚â‚€ S = -(â‹ƒâ‚€ (compl ' S)) :=
by rewrite [-compl_compl, compl_sInter]

theorem inter_sUnion_nonempty_of_inter_nonempty {s t : set X} {S : set (set X)} (Hs : t âˆˆ S) (Hne : s âˆ© t â‰  âˆ…) :
        s âˆ© â‹ƒâ‚€ S â‰  âˆ… :=
  obtain x Hsx Htx, from exists_mem_of_ne_empty Hne,
  have x âˆˆ â‹ƒâ‚€ S, from mem_sUnion Htx Hs,
  ne_empty_of_mem (mem_inter Hsx this)

theorem sUnion_inter_nonempty_of_inter_nonempty {s t : set X} {S : set (set X)} (Hs : t âˆˆ S) (Hne : t âˆ© s â‰  âˆ…) :
        (â‹ƒâ‚€ S) âˆ© s â‰  âˆ… :=
  obtain x Htx Hsx, from exists_mem_of_ne_empty Hne,
  have x âˆˆ â‹ƒâ‚€ S, from mem_sUnion Htx Hs,
  ne_empty_of_mem (mem_inter this Hsx)

-- Union and Inter: a family of sets indexed by a type

theorem Union_subset {I : Type} {b : I â†’ set X} {c : set X} (H : âˆ€ i, b i âŠ† c) : (â‹ƒ i, b i) âŠ† c :=
take x,
suppose x âˆˆ Union b,
obtain i (Hi : x âˆˆ b i), from this,
show x âˆˆ c, from H i Hi

theorem subset_Inter {I : Type} {b : I â†’ set X} {c : set X} (H : âˆ€ i, c âŠ† b i) : c âŠ† â‹‚ i, b i :=
Î» x cx i, H i cx

theorem Union_eq_sUnion_image {X I : Type} (s : I â†’ set X) : (â‹ƒ i, s i) = â‹ƒâ‚€ (s ' univ) :=
ext (take x, iff.intro
  (suppose x âˆˆ Union s,
    obtain i (Hi : x âˆˆ s i), from this,
    mem_sUnion Hi (mem_image_of_mem s trivial))
  (suppose x âˆˆ sUnion (s ' univ),
    obtain t [(Ht : t âˆˆ s ' univ) (Hx : x âˆˆ t)], from this,
    obtain i [univi (Hi : s i = t)], from Ht,
    exists.intro i (show x âˆˆ s i, by rewrite Hi; apply Hx)))

theorem Inter_eq_sInter_image {X I : Type} (s : I â†’ set X) : (â‹‚ i, s i) = â‹‚â‚€ (s ' univ) :=
ext (take x, iff.intro
  (assume H : x âˆˆ Inter s,
    take t,
    suppose t âˆˆ s 'univ,
    obtain i [univi (Hi : s i = t)], from this,
    show x âˆˆ t, by rewrite -Hi; exact H i)
  (assume H : x âˆˆ â‹‚â‚€ (s ' univ),
    take i,
    have s i âˆˆ s ' univ, from mem_image_of_mem s trivial,
    show x âˆˆ s i, from H this))

theorem compl_Union {X I : Type} (s : I â†’ set X) : - (â‹ƒ i, s i) = (â‹‚ i, - s i) :=
by rewrite [Union_eq_sUnion_image, compl_sUnion, -image_comp, -Inter_eq_sInter_image]

theorem compl_Inter {X I : Type} (s : I â†’ set X) : -(â‹‚ i, s i) = (â‹ƒ i, - s i) :=
by rewrite [Inter_eq_sInter_image, compl_sInter, -image_comp, -Union_eq_sUnion_image]

theorem Union_eq_comp_Inter_comp {X I : Type} (s : I â†’ set X) : (â‹ƒ i, s i) = - (â‹‚ i, - s i) :=
by rewrite [-compl_compl, compl_Union]

theorem Inter_eq_comp_Union_comp {X I : Type} (s : I â†’ set X) : (â‹‚ i, s i) = - (â‹ƒ i, -s i) :=
by rewrite [-compl_compl, compl_Inter]

lemma inter_distrib_Union_left {X I : Type} (s : I â†’ set X) (a : set X) :
  a âˆ© (â‹ƒ i, s i) = â‹ƒ i, a âˆ© s i :=
ext (take x, iff.intro
  (assume H, obtain i Hi, from and.elim_right H,
    have x âˆˆ a âˆ© s i, from and.intro (and.elim_left H) Hi,
    show _, from exists.intro i this)
  (assume H, obtain i [xa xsi], from H,
   show _, from and.intro xa (exists.intro i xsi)))

section
  open classical

  lemma union_distrib_Inter_left {X I : Type} (s : I â†’ set X) (a : set X) :
    a âˆª (â‹‚ i, s i) = â‹‚ i, a âˆª s i :=
  ext (take x, iff.intro
    (assume H, or.elim H
      (assume H1, take i, or.inl H1)
      (assume H1, take i, or.inr (H1 i)))
    (assume H,
      by_cases
        (suppose x âˆˆ a, or.inl this)
        (suppose x âˆ‰ a, or.inr (take i, or.resolve_left (H i) this))))
end

-- these are useful for turning binary union / intersection into countable ones

definition bin_ext (s t : set X) (n : â„•) : set X :=
nat.cases_on n s (Î» m, t)

lemma Union_bin_ext (s t : set X) : (â‹ƒ i, bin_ext s t i) = s âˆª t :=
ext (take x, iff.intro
  (assume H,
    obtain i (Hi : x âˆˆ (bin_ext s t) i), from H,
    by cases i; apply or.inl Hi; apply or.inr Hi)
  (assume H,
    or.elim H
      (suppose x âˆˆ s, exists.intro 0 this)
      (suppose x âˆˆ t, exists.intro 1 this)))

lemma Inter_bin_ext (s t : set X) : (â‹‚ i, bin_ext s t i) = s âˆ© t :=
ext (take x, iff.intro
  (assume H, and.intro (H 0) (H 1))
  (assume H, by intro i; cases i;
    apply and.elim_left H; apply and.elim_right H))

-- bUnion and bInter: a family of sets indexed by a set ("b" is for bounded)

variable {Y : Type}

theorem mem_bUnion {s : set X} {f : X â†’ set Y} {x : X} {y : Y}
    (xs : x âˆˆ s) (yfx : y âˆˆ f x) :
  y âˆˆ â‹ƒ x âˆˆ s, f x :=
exists.intro x (and.intro xs yfx)

theorem mem_bInter {s : set X} {f : X â†’ set Y} {y : Y} (H : âˆ€â‚€ x âˆˆ s, y âˆˆ f x) :
  y âˆˆ â‹‚ x âˆˆ s, f x :=
H

theorem bUnion_subset {s : set X} {t : set Y} {f : X â†’ set Y} (H : âˆ€â‚€ x âˆˆ s, f x âŠ† t) :
  (â‹ƒ x âˆˆ s, f x) âŠ† t :=
take y, assume Hy,
obtain x [xs yfx], from Hy,
show y âˆˆ t, from H xs yfx

theorem subset_bInter {s : set X} {t : set Y} {f : X â†’ set Y} (H : âˆ€â‚€ x âˆˆ s, t âŠ† f x) :
  t âŠ† â‹‚ x âˆˆ s, f x :=
take y, assume yt, take x, assume xs, H xs yt

theorem subset_bUnion_of_mem {s : set X} {f : X â†’ set Y} {x : X} (xs : x âˆˆ s) :
  f x âŠ† â‹ƒ x âˆˆ s, f x :=
take y, assume Hy, mem_bUnion xs Hy

theorem bInter_subset_of_mem {s : set X} {f : X â†’ set Y} {x : X} (xs : x âˆˆ s) :
  (â‹‚ x âˆˆ s, f x) âŠ† f x :=
take y, assume Hy, Hy x xs

theorem bInter_empty (f : X â†’ set Y) : (â‹‚ x âˆˆ (âˆ… : set X), f x) = univ :=
eq_univ_of_forall (take y x xine, absurd xine !not_mem_empty)

theorem bInter_singleton (a : X) (f : X â†’ set Y) : (â‹‚ x âˆˆ '{a}, f x) = f a :=
ext (take y, iff.intro
  (assume H, H a !mem_singleton)
  (assume H, Î» x xa, by rewrite [eq_of_mem_singleton xa]; apply H))

theorem bInter_union (s t : set X) (f : X â†’ set Y) :
  (â‹‚ x âˆˆ s âˆª t, f x) = (â‹‚ x âˆˆ s, f x) âˆ© (â‹‚ x âˆˆ t, f x) :=
ext (take y, iff.intro
  (assume H, and.intro (Î» x xs, H x (or.inl xs)) (Î» x xt, H x (or.inr xt)))
  (assume H, Î» x xst, or.elim (xst) (Î» xs, and.left H x xs) (Î» xt, and.right H x xt)))

theorem bInter_insert (a : X) (s : set X) (f : X â†’ set Y) :
  (â‹‚ x âˆˆ insert a s, f x) = f a âˆ© (â‹‚ x âˆˆ s, f x) :=
by rewrite [insert_eq, bInter_union, bInter_singleton]

theorem bInter_pair (a b : X) (f : X â†’ set Y) :
  (â‹‚ x âˆˆ '{a, b}, f x) = f a âˆ© f b :=
by rewrite [*bInter_insert, bInter_empty, inter_univ]

theorem bUnion_empty (f : X â†’ set Y) : (â‹ƒ x âˆˆ (âˆ… : set X), f x) = âˆ… :=
eq_empty_of_forall_not_mem (Î» y H, obtain x [xine yfx], from H,
  !not_mem_empty xine)

theorem bUnion_singleton (a : X) (f : X â†’ set Y) : (â‹ƒ x âˆˆ '{a}, f x) = f a :=
ext (take y, iff.intro
  (assume H, obtain x [xina yfx], from H,
    show y âˆˆ f a, by rewrite [-eq_of_mem_singleton xina]; exact yfx)
  (assume H, exists.intro a (and.intro !mem_singleton H)))

theorem bUnion_union (s t : set X) (f : X â†’ set Y) :
  (â‹ƒ x âˆˆ s âˆª t, f x) = (â‹ƒ x âˆˆ s, f x) âˆª (â‹ƒ x âˆˆ t, f x) :=
ext (take y, iff.intro
  (assume H, obtain x [xst yfx], from H,
    or.elim xst
      (Î» xs, or.inl (exists.intro x (and.intro xs yfx)))
      (Î» xt, or.inr (exists.intro x (and.intro xt yfx))))
  (assume H, or.elim H
    (assume H1, obtain x [xs yfx], from H1,
      exists.intro x (and.intro (or.inl xs) yfx))
    (assume H1, obtain x [xt yfx], from H1,
      exists.intro x (and.intro (or.inr xt) yfx))))

theorem bUnion_insert (a : X) (s : set X) (f : X â†’ set Y) :
  (â‹ƒ x âˆˆ insert a s, f x) = f a âˆª (â‹ƒ x âˆˆ s, f x) :=
by rewrite [insert_eq, bUnion_union, bUnion_singleton]

theorem bUnion_pair (a b : X) (f : X â†’ set Y) :
  (â‹ƒ x âˆˆ '{a, b}, f x) = f a âˆª f b :=
by rewrite [*bUnion_insert, bUnion_empty, union_empty]

end set

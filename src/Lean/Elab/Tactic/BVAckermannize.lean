/-
Copyright (c) 2024 Siddharth Bhat. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Siddharth Bhat

This file implements strict ackermannization [1, 2]

[1] https://lara.epfl.ch/w/_media/model-based.pdf
[2] https://leodemoura.github.io/files/oregon08.pdf
[3] https://github.com/Z3Prover/z3/blob/d047b86439ec209446d211f0f6b251ebfba070d8/src/ackermannization/lackr.cpp#L206
[4] https://github.com/Z3Prover/z3/blob/d047b86439ec209446d211f0f6b251ebfba070d8/src/ackermannization/lackr_model_constructor.cpp#L344
-/
prelude
import Lean.Expr
import Lean.Message
import Std.Tactic.BVDecide.Bitblast
import Std.Tactic.BVAckermannize.Syntax
import Lean.Meta.Basic
import Lean.Elab.Tactic.Basic
import Lean.Meta.LitValues
import Lean.Meta.InferType
import Lean.Elab.Tactic.FalseOrByContra
import Lean.Meta.Tactic.Assert
import Lean.Meta.Tactic.Util
import Lean.Meta.Tactic.Congr
import Lean.Meta.Tactic.Replace

open Lean Elab Meta Tactic

initialize Lean.registerTraceClass `Meta.Tactic.bv_ack

namespace Ack

 /-- Types that can be bitblasted by bv_decide. -/
 inductive BVTy
   /-- Booleans. -/
   | Bool
   /-- Bitvectors of a fixed width `w`. -/
   | BitVec (w : Nat)
   deriving Hashable, DecidableEq, Inhabited

instance : ToMessageData BVTy where
  toMessageData
  | .Bool => m!"bool"
  | .BitVec w => m!"BitVec {w}"

namespace BVTy

/-- Reify a raw expression of type `Type` into the types of bitvectors we can bitblast,
returning `none` if `e` was not recognized as either `Bool` or `BitVec ?w`,
with `?w` a literal `Nat`.  -/
def ofExpr? (e : Expr) : MetaM (Option BVTy) := ofExpr?Aux e |>.run where
  ofExpr?Aux (e : Expr) : OptionT MetaM BVTy :=
    match_expr e.consumeMData with
    | _root_.Bool => return Bool
    | _root_.BitVec w => do
       let w ‚Üê getNatValue? w
       return .BitVec w
    | _ => OptionT.fail

/-- Convert a `BVTy` back into an `Expr`. -/
def toExpr : BVTy ‚Üí Expr
| .Bool => mkConst ``_root_.Bool
| .BitVec w => mkApp (mkConst ``_root_.BitVec) (mkNatLit w)

end BVTy

/-- An argument to an uninterpreted function, which we track for ackermannization. -/
structure Argument where
  /-- The expression corresponding to the argument. -/
  value : Expr
  /-- The type of the argument. -/
  type : BVTy
deriving Hashable, BEq, Inhabited

namespace Argument

instance : ToMessageData Argument where
  toMessageData arg := m!"{arg.value}"
end Argument

/--
A function, which packs the expression and the type of the codomain.
We use the type of the codomain to build an abstracted value for every call of this function.
-/
structure Function where
  /-- The function -/
  f : Expr
  /-- The type of the function's codomain -/
  codomain : BVTy
 deriving Hashable, BEq, Inhabited

namespace Function

instance : ToMessageData Function where
  toMessageData fn := m!"{fn.f} (cod: {fn.codomain})"

end Function

/--
NOTE: is it sensible to hash an array of arguments?
We may want to use something like a trie to index these.
Consider swiching to something like `Trie`.
-/
abbrev ArgumentArray := Array Argument

/-- The data stored for an ackermannized call to allow us to build proofs. -/
structure CallInfo where
  /-- The free variable `ack_fx‚ÇÅ...x‚Çô := (f x‚ÇÅ x‚ÇÇ ... x‚Çô)`. -/
  fvar : FVarId
  /-- heqProof : The proof that `ack_fx‚ÇÅ...fx‚Çô = f x‚ÇÅ x‚ÇÇ ... x‚Çô` (name/fvar = value/expr). -/
  heqProof : Expr
deriving Hashable, BEq, Inhabited

namespace CallInfo

instance : ToMessageData CallInfo where
  toMessageData cv := m!"{Expr.fvar cv.fvar} ({cv.heqProof})"

end CallInfo
structure State where
  /--
  A mapping from a function `f`, to a map of arguments `x‚ÇÅ ... x‚Çô`, to the information stored about the call.
  This is used to generate equations of the form `x‚ÇÅ = y‚ÇÅ ‚Üí x‚ÇÇ = y‚ÇÇ ‚Üí ... ‚Üí x‚Çô = y‚Çô ‚Üí ack_fx‚ÇÅ...x‚Çô = ack_fy‚ÇÅ...y‚Çô.
  -/
  fn2args2call : Std.HashMap Function (Std.HashMap ArgumentArray CallInfo) := {}
  /-- A counter for generating fresh names. -/
  gensymCounter : Nat := 0

def State.init : State where

abbrev AckM := StateRefT State MetaM

namespace AckM

def run (m : AckM Œ±) : MetaM Œ± := m.run' State.init

/-- Generate a fresh name. -/
def gensym : AckM Name := do
  modify fun s => { s with gensymCounter := s.gensymCounter + 1 }
  return Name.mkNum (Name.mkSimple "ackConst") (‚Üê get).gensymCounter

def withContext (g : MVarId) (ma : AckM Œ±) : AckM Œ± := g.withContext ma

/-- Get the map from an argument list to a call of a function `fn`. -/
def _getCallMap (fn : Function) : AckM (Std.HashMap ArgumentArray CallInfo) := do
  return (‚Üê get).fn2args2call.getD fn {}

/-- Get the calls to a function `fn`. -/
def getCallVal? (fn : Function) (args : Array Argument) : AckM (Option CallInfo) := do
  return (‚Üê _getCallMap fn).get? args

structure IntroDefResult where
  -- the new name/fvar of the defn.
  defn : FVarId
  -- a proof 'hdefn : name = value
  eqProof : FVarId

/-
Introduce a new definition with name `name : hdefTy` into the local context,
and return the FVarId of the new definition in the new goal (the MVarId) returned.
-/
def introDefExt (g : MVarId) (name : Name) (hdefTy : Expr) (hdefVal : Expr) : AckM (IntroDefResult √ó MVarId) := do
  withContext g do
    let g ‚Üê g.assertExt name (hName := Name.str name "value") hdefTy hdefVal
    let (defn, g) ‚Üê g.intro1P
    let (eqProof, g) ‚Üê g.intro1P
    return ({ defn, eqProof}, g)

/-- Insert the CallInfo `cv` at `(fn, args)` into the state. -/
private def _insertCallVal (fn : Function) (args : ArgumentArray) (cv : CallInfo) : AckM Unit := do
  let calls ‚Üê _getCallMap fn
  modify fun s => { s with fn2args2call := s.fn2args2call.insert fn (calls.insert args cv) }

/--
Replace a call `f x‚ÇÅ ... x‚Çô` with a new free variable `fv` where `fv := f x‚ÇÅ ... x‚Çô`.
This free variable `fv` is cached, and represented by a `CallVal`.
Moreover, Since the `fv` is defeq to `f x‚ÇÅ ... x‚Çô, we can substitute `fv` for `f x‚ÇÅ ... x‚Çô`.
-/
def insertOrLookupCall (g : MVarId) (fn : Function) (args : ArgumentArray) : AckM (CallInfo √ó MVarId) := do
  g.withContext do
    if let some val ‚Üê getCallVal? fn args then
      trace[Meta.Tactic.bv_ack] "using cached call {val} for {fn} {args}"
      return (val, g)
    let e := mkAppN fn.f (args.map Argument.value)
    let name ‚Üê gensym
    let (introDef, g) ‚Üê introDefExt g name fn.codomain.toExpr e
    let cv := { fvar := introDef.defn, heqProof := Expr.fvar introDef.eqProof : CallInfo }
    _insertCallVal fn args cv
    return (cv, g)

/-- Create a trace node in trace class (i.e. `set_option traceClass true`),
with header `header`, whose default collapsed state is `collapsed`. -/
def withTraceNode (g : MVarId) (header : MessageData) (k : AckM Œ±)
    (collapsed : Bool := true)
    (traceClass : Name := `Meta.Tactic.bv_ack) : AckM Œ± :=
  withContext g do Lean.withTraceNode traceClass (fun _ => return header) k (collapsed := collapsed)

/--
The result of running ackermannization on an expression. Returns the ackermannized expression,
as well as informing whether this expression can be used as a subexpression for further ackermannization,
by tracking whether the expression has loose bound variables.
-/
structure AckResult where
  /-- The resulting expression from ackermannization. -/
  val : Expr
  /--
  Whether an expression has bound variables in it.
  If it does, then it cannot be used as the subexpression of an ackermanized call,
  since the expression is not zeroth-order.
  -/
  hasLooseBvar : Bool
deriving Inhabited


/--
Given a binary effectful computation `f : Œ± ‚Üí Œ≤ ‚Üí m Œ≥`, and two arrays `as : Array Œ±` and `bs : Array Œ≤`,
build an array whose elements are created by invoking `f as[i] bs[i]` in sequence.
This obeys the equation `zipWithM as bs f = as.zip bs |>.mapM f
-/
private def _root_.Array.zipWithM [Monad m] (as : Array Œ±) (bs : Array Œ≤) (f : Œ± ‚Üí Œ≤ ‚Üí m Œ≥) : m (Array Œ≥) := do
  let mut out := #[]
  for (a, b) in as.zip bs do
    out := out.push (‚Üê f a b)
  return out

mutual

/--
Try to ackermannize an application. Recursively ackermannize all of its subexpressions,
and try to ackermannize the application if:
- Neither the function `f` nor the arguments `x‚ÇÅ` ... `x‚Çô` have bound variables in them.
- `f` has a type that is ackermannizable: all arguments and output are either `BitVec w` or `Bool`.
-/
partial def ackApp (g : MVarId) (app : Expr) : AckM (AckResult √ó MVarId) := do
  g.withContext do
    let f := app.getAppFn
    let (f, g) ‚Üê introAckForExpr g f
    let mut hasLooseBvar := f.hasLooseBvar
    let mut args : Array Expr := #[]
    let mut g := g

    for arg in app.getAppArgs do
      let (out, g') ‚Üê g.withContext do introAckForExpr g arg
      g := g'
      args := args.push out.val
      hasLooseBvar := hasLooseBvar || out.hasLooseBvar
      -- If we have loose bvars, then we give up on ackermannizing.
      if hasLooseBvar then continue
    -- `nonAckRet` is the return value in case we fail to ackermannize the call.
    let nonAckRet := ({ val := mkAppN f.val args, hasLooseBvar }, g)
    -- We have loose bvars, so bail out.
    if hasLooseBvar then return nonAckRet
    
    -- We know that we have no loose bvars, so it's legal to call inferType.
    let resultType ‚Üê inferType app
    let some codomain ‚Üê g.withContext do BVTy.ofExpr? resultType | return nonAckRet
    -- The function is ackermanizable.
    let ackFn := { f := f.val, codomain : Function }
    -- If all our arguments were legal ackermannization arguments, then
    -- we can ackermannize. If not, we bail out.
    let mut ackArgs := #[]
    for arg in args do
      -- We know that we have no loose bvars, so it's legal to call inferType.
      -- Check if the type is a bitvector type.
      let some ty ‚Üê g.withContext do BVTy.ofExpr? (‚Üê inferType arg) | return nonAckRet
      ackArgs := ackArgs.push <| Argument.mk arg ty
    -- ackermannize our call.
    let (call, g') ‚Üê g.withContext do insertOrLookupCall g ackFn ackArgs
    g := g'
    withContext g do trace[Meta.Tactic.bv_ack] "{checkEmoji} {app} ‚Üí {call}."
    let val := Expr.fvar call.fvar
    return ({ val, hasLooseBvar := false }, g)

/--
Traverse the expression 'e', and ackermannize potential subexpressions.
We explicitly do not want to use `forallTelescoping` and family,
because we want to only work with the zeroth order fragment (predicate logic),
and ignore anything that is first (and higher) order.

Hence, we use an `AckResult`, which keeps track of whether the resulting
expression has a loose bvar.

If the expression `e` is an application `f x‚ÇÅ ... x‚Çô`, and none of `f`, `x‚ÇÅ`, ..., `x‚Çô`
have loose bvars, we try to ackermannize at `ackApp`.
If the application does have a bvar, we do not ackermannize.

This invariant is maintained by `ackApp`, and `introAckForExpr` visits
expressions and ackermannizes them in turn.
-/

partial def introAckForExpr (g : MVarId) (e : Expr) : AckM (AckResult √ó MVarId) :=
  withContext g do
    withTraceNode g (m!"üéØ {e}") (collapsed := false) do
    match e with
    | .mdata _ e => introAckForExpr g e
    | .bvar _ => return ({ val := e, hasLooseBvar := true }, g)
    | .proj tyName struct e =>
      let (out, g) ‚Üê introAckForExpr g e
      return ({ val := .proj tyName struct out.val, hasLooseBvar := out.hasLooseBvar}, g)
    | .fvar .. | .mvar .. | .sort .. | .const .. |  .lit .. => do
      return ({ val := e, hasLooseBvar := false }, g)
    | .lam _binderName binderTy body _binderInfo =>
        let (binderTy, g) ‚Üê introAckForExpr g binderTy
        let (body, g) ‚Üê introAckForExpr g body
        let val := e.updateLambdaE! binderTy.val body.val
        let hasLooseBvar := binderTy.hasLooseBvar || body.hasLooseBvar
        return ({ val, hasLooseBvar}, g)
    | .letE _declName type value body _nonDep =>
        let (type, g) ‚Üê introAckForExpr g type
        let (value, g) ‚Üê introAckForExpr g value
        let (body, g) ‚Üê introAckForExpr g body
        let val := e.updateLet! type.val value.val body.val
        let hasLooseBvar := type.hasLooseBvar || value.hasLooseBvar || body.hasLooseBvar
        return ({ val, hasLooseBvar}, g)
    | .forallE _binderName binderTy body _binderInfo =>
        let (binderTy, g) ‚Üê introAckForExpr g binderTy
        let (body, g) ‚Üê introAckForExpr g body
        let val := e.updateForallE! binderTy.val body.val
        let hasLooseBvar := binderTy.hasLooseBvar || body.hasLooseBvar
        return ({ val, hasLooseBvar}, g)
    | .app .. => withTraceNode g m!"üéØ Expr.app '{e}'" (collapsed := false) do
      ackApp g e
end

/--
Return true if the argument lists are trivially different.
This is an optimization that we do not yet implement.
-/
def areArgListsTriviallyDifferent (_arg‚ÇÅ _arg‚ÇÇ : Array Argument) : AckM Bool := return false

/-
Return true if the argument lists are trivially the same.
This is an optimization that we do not yet implement.
If possible, return the simplified hypothesis of the equality of these expressions.
TODO: -- def areArgListsTriviallySame (arg‚ÇÅ arg‚ÇÇ : Array Argument) : AckM (Option Expr) := return none
-/


/-- info: congr.{u, v} {Œ± : Sort u} {Œ≤ : Sort v} {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±} (h‚ÇÅ : f‚ÇÅ = f‚ÇÇ) (h‚ÇÇ : a‚ÇÅ = a‚ÇÇ) : f‚ÇÅ a‚ÇÅ = f‚ÇÇ a‚ÇÇ -/
#guard_msgs in #check congr

/-- This is how we build the congruence lemma for `n` arguments. -/
private example (f : X1 ‚Üí X2 ‚Üí O)
    -- we have these already.
    (x1 x1' : X1) (x2 x2' : X2)
    (ackEqApp : fx1x2 = f x1 x2)
    (ackEqApp' : fx1'x2' = f x1' x2')
    -- to be intros'd
    (h1 : x1 = x1')
    (h2 : x2 = x2') :
  fx1x2 = fx1'x2' :=
  let appEqApp : f x1 x2 = f x1' x2' := congr (congr (Eq.refl f) h1) h2
  Eq.trans (Eq.trans ackEqApp appEqApp) (Eq.symm ackEqApp')

/--
Make the ackermannization theorem, which states that: `(‚àÄ i, arg‚ÇÅ[i] = arg‚ÇÇ[i]) -> call‚ÇÅ = call‚ÇÇ`.
Formally, we build an expr such as `arg‚ÇÅ = arg'‚ÇÅ -> arg‚ÇÇ = arg'‚ÇÇ -> ... arg‚Çô = arg'‚Çô -> call‚ÇÅ = call‚ÇÇ`,
where the proof is by congruence over the equalities.
-/
def mkAckThm (g : MVarId) (fn : Function) (args args' : Array Argument) (call call' : CallInfo) : AckM MVarId := do
  withContext g do
    trace[Meta.Tactic.bv_ack] "making ack congr thm for '{fn}' '{args}' ~ '{args'}',  calls '{call}', '{call'}'"
    if args.size = 0 then
      throwTacticEx `bv_ack g
        m!"expected {args} to have more than zero arguments when building congr theorem for {fn}."
    if args'.size = 0 then
      throwTacticEx `bv_ack g
        m!"expected {args'} to have more than zero arguments when building congr theorem for {fn}."

    if args.size ‚â† args'.size then
      throwTacticEx `bv_ack g
        m!"internal error: expected {args} to have the same size as {args'} when building congr thm for {fn}."

    let mut eqHyps := #[]
    for (arg, arg') in args.zip args' do
      eqHyps := eqHyps.push (‚Üê mkEq arg.value arg'.value)
    -- we build the equality according to the example above.
    let mut localDecls : Array (Name √ó BinderInfo √ó (Array Expr ‚Üí AckM Expr)) := #[]
    let mut i := 0
    for (arg, arg') in args.zip args' do
      let name := Name.num (Name.mkSimple "ack_arg") i
      localDecls := localDecls.push (name, BinderInfo.default, fun _ => mkEq arg.value arg'.value)
    let ackEqn ‚Üê g.withContext <| withLocalDecls localDecls fun argsEq => do
      let mut fEq ‚Üê mkEqRefl fn.f
      for argEq in argsEq do
        fEq ‚Üê mkCongr fEq argEq

      let finalEq ‚Üê mkEqTrans (‚Üê mkEqTrans call.heqProof fEq) (‚Üê mkEqSymm call'.heqProof)
      mkLambdaFVars argsEq  finalEq
    withContext g do trace[Meta.Tactic.bv_ack] "made ackermann equation: {ackEqn}"
    let (_ackEqn, g) ‚Üê g.note (Name.mkSimple s!"ackEqn{fn.f}") ackEqn
    return g

/--
For every bitvector (x : BitVec w), for every function `(f : BitVec w ‚Üí BitVec w')`,
walk every function application (f x), and add a new fvar (fx : BitVec w').
- Keep an equality that says `fx = f x`.
- For function application of f, for each pair of bitvectors x, y,
  add a hypothesis that says `x = y => fx = fy, with proof.

FUTUREWORK: This can be extended to work with functions where only the output type is bitblastable,
so this will allow us to ackemannize equations such as `f : Nat ‚Üí BitVec w ‚Üí BitVec w`, with `(f 0) x` and `(f 0) y`
being correctly handled. That is, we can add the equation `x = y ‚Üí f 0 x = f 0 y`.
-/
def ack (g : MVarId) : AckM MVarId := do
  g.withContext do
    let target ‚Üê g.getType
    let (target', g) ‚Üê introAckForExpr g (‚Üê g.getType)
    let g ‚Üê g.replaceTargetDefEq target'.val
    let hyps ‚Üê g.getNondepPropHyps

    let mut g := g
    for hyp in hyps do
      g ‚Üê g.withContext do
        withTraceNode g m!"üéØ hyp '{‚Üê hyp.getType}'" (collapsed := false) do
          let (_hyp, g) ‚Üê introAckForExpr g (‚Üê hyp.getType)
          pure g

    for (fn, arg2call) in (‚Üê get).fn2args2call do
      let argCallsArr := arg2call.toArray
      for hi : i in [0:argCallsArr.size] do
        let (arg‚ÇÅ, call‚ÇÅ) := argCallsArr[i]
        for hj : j in [i+1:argCallsArr.size] do
          let (arg‚ÇÇ, call‚ÇÇ) := argCallsArr[j]
          if ‚Üê areArgListsTriviallyDifferent arg‚ÇÅ arg‚ÇÇ then continue
          g ‚Üê mkAckThm g fn arg‚ÇÅ arg‚ÇÇ call‚ÇÅ call‚ÇÇ
    withContext g do trace[Meta.Tactic.bv_ack] "{checkEmoji} ack.{indentD g}"
    return g

end AckM

/-- Entry point for programmatic usage of `bv_ackermannize`. -/
def ackTac : TacticM Unit := do
  withoutRecover do
    liftMetaTactic fun g => do
      let g ‚Üê (AckM.ack g).run
      return [g]

end Ack

@[builtin_tactic Lean.Parser.Tactic.bvAckEager]
def evalBvAckEager : Tactic := fun
  | `(tactic| bv_ack_eager) =>
    Ack.ackTac
  | _ => throwUnsupportedSyntax

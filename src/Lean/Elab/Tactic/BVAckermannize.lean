/-
Copyright (c) 2024 Siddharth Bhat. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Siddharth Bhat

This file implements strict ackermannization [1, 2]

[1] https://lara.epfl.ch/w/_media/model-based.pdf
[2] https://leodemoura.github.io/files/oregon08.pdf
[3] https://github.com/Z3Prover/z3/blob/d047b86439ec209446d211f0f6b251ebfba070d8/src/ackermannization/lackr.cpp#L206
[4] https://github.com/Z3Prover/z3/blob/d047b86439ec209446d211f0f6b251ebfba070d8/src/ackermannization/lackr_model_constructor.cpp#L344
-/
prelude
import Lean.Expr
import Lean.Message
import Std.Tactic.BVDecide.Bitblast
import Std.Tactic.BVAckermannize.Syntax
import Lean.Meta.Basic
import Lean.Elab.Tactic.Basic
import Lean.Meta.LitValues
import Lean.Meta.InferType
import Lean.Elab.Tactic.FalseOrByContra
import Lean.Meta.Tactic.Assert
import Lean.Meta.Tactic.Util
import Lean.Meta.Tactic.Congr
import Lean.Meta.Tactic.Replace

open Lean Elab Meta Tactic

initialize Lean.registerTraceClass `Meta.Tactic.bv_ack

namespace Ack

 /-- Types that can be bitblasted by bv_decide -/
 inductive BVTy
   /-- Booleans -/
   | Bool
   /-- Bitvectors of a fixed width `w` -/
   | BitVec (w : Nat)
   deriving Hashable, DecidableEq, Inhabited

instance : ToMessageData BVTy where
  toMessageData
  | .Bool => m!"bool"
  | .BitVec w => m!"BitVec {w}"

namespace BVTy

/-- Reify a raw expression of type `Type` into the types of bitvectors we can bitblast,
returning `none` if `e` was not recognized as either `Bool` or `BitVec ?w`,
with `?w` a literal `Nat`  -/
def ofExpr? (e : Expr) : OptionT MetaM BVTy :=
  match_expr e.consumeMData with
  | _root_.Bool => return Bool
  | _root_.BitVec w => do
     let w ‚Üê getNatValue? w
     return .BitVec w
  | _ => OptionT.fail

/-- Convert a `BVTy` back into an `Expr` -/
def toExpr : BVTy ‚Üí Expr
| .Bool => mkConst ``_root_.Bool
| .BitVec w => mkApp (mkConst ``_root_.BitVec) (mkNatLit w)

end BVTy

/-- An argument to an uninterpreted function, which we track for ackermannization. -/
structure Argument where
  /-- The expression corresponding to the argument -/
  value : Expr
  /-- The cached type of the expression x -/
  type : BVTy
deriving Hashable, BEq, Inhabited

namespace Argument

instance : ToMessageData Argument where
  toMessageData arg := m!"{arg.value} : {arg.type}"

/-- Build an `Argument` from a raw expression of type `Bool` or `BitVec _`. -/
def ofExpr? (e : Expr) : OptionT MetaM Argument := do
  let t ‚Üê BVTy.ofExpr? (‚Üê inferType e)
  return { value := e, type := t}

end Argument

/--
A function, which packs the expression and the type of the codomain of the function.
We use the type of the codomain to build an abstracted value for every call of this function.
-/
structure Function where
  /-- The function -/
  f : Expr
  /-- The type of the function's codomain -/
  codomain : BVTy
 deriving Hashable, BEq, Inhabited

namespace Function

instance : ToMessageData Function where
  toMessageData fn := m!"{fn.f} (cod: {fn.codomain})"

end Function

/--
NOTE: is it sensible to hash an array of arguments?
We may want to use something like a trie to index these.
Consider swiching to something like `Trie`.
-/
abbrev ArgumentArray := Array Argument
/--
The data stored for an ackermannized call to allow us to build proofs.
-/
structure CallVal where
  /-- The free variable `ack_fx‚ÇÅ...x‚Çô := (f x‚ÇÅ x‚ÇÇ ... x‚Çô)`. -/
  fvar : FVarId
  /-- heqProof : The proof that `ack_fx‚ÇÅ...fx‚Çô = f x‚ÇÅ x‚ÇÇ ... x‚Çô` (name/fvar = value/expr). -/
  heqProof : Expr
deriving Hashable, BEq, Inhabited

namespace CallVal

instance : ToMessageData CallVal where
  toMessageData cv := m!"{Expr.fvar cv.fvar} ({cv.heqProof})"

end CallVal
structure State where
  /--
  A mapping from a function `f`, to a map of arguments `x‚ÇÅ ... x‚Çô`, to the information stored about the call.
  This is used to generate equations of the form `x‚ÇÅ = y‚ÇÅ ‚Üí x‚ÇÇ = y‚ÇÇ ‚Üí ... ‚Üí x‚Çô = y‚Çô ‚Üí ack_fx‚ÇÅ...x‚Çô = ack_fy‚ÇÅ...y‚Çô on-demand.
  -/
  fn2args2call : Std.HashMap Function (Std.HashMap ArgumentArray CallVal) := {}
  /-- A counter for generating fresh names. -/
  gensymCounter : Nat := 0

def State.init : State where

abbrev AckM := StateRefT State MetaM

namespace AckM

def run (m : AckM Œ±) : MetaM Œ± := m.run' State.init

/-- Generate a fresh name. -/
def gensym : AckM Name := do
  modify fun s => { s with gensymCounter := s.gensymCounter + 1 }
  return Name.mkNum (Name.mkSimple "ackConst") (‚Üê get).gensymCounter

def withContext (g : MVarId) (ma : AckM Œ±) : AckM Œ± := g.withContext ma

/-- Get the calls to a function `fn`. -/
def getCallMap (fn : Function) : AckM (Std.HashMap ArgumentArray CallVal) := do
  return (‚Üê get).fn2args2call.getD fn {}

/-- Get the calls to a function `fn`. -/
def getCallVal? (fn : Function) (args : Array Argument) : AckM (Option CallVal) := do
  let calls ‚Üê getCallMap fn
  if let .some val := calls.get? args then
    return some val
  return none

structure IntroDefResult where
  -- the new name/fvar of the defn.
  defn : FVarId
  -- a proof 'hdefn : name = value
  eqProof : FVarId

/-
Introduce a new definition with name `name : hdefTy` into the local context,
and return the FVarId of the new definition in the new goal (the MVarId) returned.
-/
def introDefExt (g : MVarId) (name : Name) (hdefTy : Expr) (hdefVal : Expr) : AckM (IntroDefResult √ó MVarId) := do
  withContext g do
    let g ‚Üê g.assertExt name (hName := Name.str name "value") hdefTy hdefVal
    let (defn, g) ‚Üê g.intro1P
    let (eqProof, g) ‚Üê g.intro1P
    return ({ defn, eqProof}, g)

/-- Insert the CallVal `cv` at `(fn, args)` into the state. -/
private def _insertCallVal (fn : Function) (args : ArgumentArray) (cv : CallVal) : AckM Unit := do
  let calls ‚Üê getCallMap fn
  modify fun s => { s with fn2args2call := s.fn2args2call.insert fn (calls.insert args cv) }

/--
Replace a call to the function `f` with an `fvar`. Since the `fvar` is defeq to the call,
we can just replace occurrences of the call with the fvar `f`.

We will later add another hypothesis with the equality that `fvar = f x‚ÇÅ ... x‚Çô`
-/
def replaceCallWithFVar (g : MVarId) (fn : Function) (args : ArgumentArray) : AckM (CallVal √ó MVarId) := do
  g.withContext do
    if let some val ‚Üê getCallVal? fn args then
      trace[bv_ack] "using cached call {val} for {fn} {args}"
      return (val, g)
    let e := mkAppN fn.f (args.map Argument.value)
    let name ‚Üê gensym
    let (introDef, g) ‚Üê introDefExt g name fn.codomain.toExpr e
    let cv := { fvar := introDef.defn, heqProof := Expr.fvar introDef.eqProof : CallVal }
    _insertCallVal fn args cv
    return (cv, g)

/-- Create a trace node in trace class (i.e. `set_option traceClass true`),
with header `header`, whose default collapsed state is `collapsed`. -/
def withTraceNode (header : MessageData) (k : AckM Œ±)
    (collapsed : Bool := true)
    (traceClass : Name := `bv_ack) : AckM Œ± :=
  Lean.withTraceNode traceClass (fun _ => return header) k (collapsed := collapsed)

/-- Returns `True` if the type is a function type that is understood by the bitblaster. -/
def isBitblastTy (e : Expr) : Bool :=
  match_expr e with
  | BitVec _ => true
  | Bool => true
  | _ => false

mutual
partial def ackAppChildren (g : MVarId) (e : Expr) : AckM (Expr √ó MVarId) := do
  g.withContext do
    trace[bv_ack] "{crossEmoji} bailing out on illegal application {e}"
    let f := e.getAppFn
    let (f, g) ‚Üê introAckForExpr g f
    -- NOTE: Rewrite as a fold? It's not too much cleaner, sadly.
    let mut args := #[]
    let mut g := g
    for arg in e.getAppArgs do
      let gArg ‚Üê introAckForExpr g arg
      g := gArg.2
      args := args.push gArg.1
    return (mkAppN f args, g) -- NOTE: is there some way to use update to update this?


partial def introAckForExpr (g : MVarId) (e : Expr) : AckM (Expr √ó MVarId) := do
  Lean.withTraceNode `bv_ack (fun _ => g.withContext do pure m!"üéØ {e}") (collapsed := false) do
    match e with
    | .mdata _ e => introAckForExpr g e
    | .bvar .. | .fvar .. | .mvar .. | .sort .. | .const .. | .proj .. | .lit .. => return (e, g)
    | .lam _binderName binderTy body _binderInfo =>
        let (binderTy, g) ‚Üê introAckForExpr g binderTy
        let (body, g) ‚Üê introAckForExpr g body
        return (e.updateLambdaE! binderTy body, g)
    | .letE _declName type value body _nonDep =>
        let (type, g) ‚Üê introAckForExpr g type
        let (value, g) ‚Üê introAckForExpr g value
        let (body, g) ‚Üê introAckForExpr g body
        return (e.updateLet! type value body, g)
    | .forallE _binderName binderTy body _binderInfo =>
        let (binderTy, g) ‚Üê introAckForExpr g binderTy
        let (body, g) ‚Üê introAckForExpr g body
        return (e.updateForallE! binderTy body, g)
    | .app .. => do
      withTraceNode m!"üéØ Expr.app '{e}'" (collapsed := false) do
        let f := e.getAppFn
        let te ‚Üê inferType e
        let .some codomain ‚Üê BVTy.ofExpr? te |>.run
          | do
            trace[bv_ack] "{crossEmoji} '{te}' not BitVec/Bool."
            return (‚Üê ackAppChildren g e)
        trace[bv_ack] "{checkEmoji} {e}'s codomain '{te}'"

        let fn := { f, codomain : Function }

        let args := e.getAppArgs
        assert! args.size > 0 -- since we are an application, we must have at least one argument.
        -- run ackermannization on all the arguments.
        -- This ensures that we process bottom-up, and at this stage, our argument
        -- has been ackermannized, if such an opportunity exists.
        let mut ackArgs := #[]
        for arg in args do
          trace[bv_ack] "üéØ arg {arg}"
          let (arg, g) ‚Üê introAckForExpr g arg
          if let .some ackArg ‚Üê Argument.ofExpr? arg |>.run then
            trace[bv_ack] "{checkEmoji} arg {arg}"
            ackArgs := ackArgs.push ackArg
          else
            -- bail out, and recurse into the `app` since we can't ackermannize it.
            trace[bv_ack] "{crossEmoji} {arg}"
            return (‚Üê ackAppChildren g e)

        let (call, g) ‚Üê replaceCallWithFVar g fn ackArgs
        trace[bv_ack] "{checkEmoji} {e} ‚Üí {call}."
        return (Expr.fvar call.fvar, g)
end

/--
Return true if the argument lists are trivially different.
This is an optimization that we do not yet implement.
-/
def areArgListsTriviallyDifferent (_arg‚ÇÅ _arg‚ÇÇ : Array Argument) : AckM Bool := return false

/-
Return true if the argument lists are trivially the same.
This is an optimization that we do not yet implement.
If possible, return the simplified hypothesis of the equality of these expressions.
TODO: -- def areArgListsTriviallySame (arg‚ÇÅ arg‚ÇÇ : Array Argument) : AckM (Option Expr) := return none
-/


/-- info: congr.{u, v} {Œ± : Sort u} {Œ≤ : Sort v} {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±} (h‚ÇÅ : f‚ÇÅ = f‚ÇÇ) (h‚ÇÇ : a‚ÇÅ = a‚ÇÇ) : f‚ÇÅ a‚ÇÅ = f‚ÇÇ a‚ÇÇ -/
#guard_msgs in #check congr

/-- This is how we build the congruence lemma for `n` arguments. -/
private example (f : X1 ‚Üí X2 ‚Üí O)
    -- we have these already.
    (x1 x1' : X1) (x2 x2' : X2)
    (ackEqApp : fx1x2 = f x1 x2)
    (ackEqApp' : fx1'x2' = f x1' x2')
    -- to be intros'd
    (h1 : x1 = x1')
    (h2 : x2 = x2') :
  fx1x2 = fx1'x2' :=
  let appEqApp : f x1 x2 = f x1' x2' := congr (congr (Eq.refl f) h1) h2
  Eq.trans (Eq.trans ackEqApp appEqApp) (Eq.symm ackEqApp')

/--
Make the ackermannization theorem, which states that: `(‚àÄ i, arg‚ÇÅ[i] = arg‚ÇÇ[i]) -> call‚ÇÅ = call‚ÇÇ`.
Formally, we build an expr such as `arg‚ÇÅ = arg'‚ÇÅ -> arg‚ÇÇ = arg'‚ÇÇ -> ... arg‚Çô = arg'‚Çô -> call‚ÇÅ = call‚ÇÇ`,
where the proof is by congruence over the equalities.
-/
def mkAckThm (g : MVarId) (fn : Function) (args args' : Array Argument) (call call' : CallVal) : AckM MVarId := do
  trace[bv_ack] "making ack congr thm for '{fn}' '{args}' ~ '{args'}',  calls '{call}', '{call'}'"
  if args.size = 0 then
    throwTacticEx `bv_ack g
      m!"expected {args} to have more than zero arguments when building congr theorem for {fn}."
  if args'.size = 0 then
    throwTacticEx `bv_ack g
      m!"expected {args'} to have more than zero arguments when building congr theorem for {fn}."

  if args.size ‚â† args'.size then
    throwTacticEx `bv_ack g
      m!"internal error: expected {args} to have the same size as {args'} when building congr thm for {fn}."

  let mut eqHyps := #[]
  for (arg, arg') in args.zip args' do
    eqHyps := eqHyps.push (‚Üê mkEq arg.value arg'.value)
  -- we build the equality according to the example above.
  let mut localDecls : Array (Name √ó BinderInfo √ó (Array Expr ‚Üí AckM Expr)) := #[]
  let mut i := 0
  for (arg, arg') in args.zip args' do
    let name := Name.num (Name.mkSimple "ack_arg") i
    localDecls := localDecls.push (name, BinderInfo.default, fun _ => mkEq arg.value arg'.value)
  let ackEqn ‚Üê g.withContext <| withLocalDecls localDecls fun argsEq => do
    let mut fEq ‚Üê mkEqRefl fn.f
    for argEq in argsEq do
      fEq ‚Üê mkCongr fEq argEq

    let finalEq ‚Üê mkEqTrans (‚Üê mkEqTrans call.heqProof fEq) (‚Üê mkEqSymm call'.heqProof)
    mkLambdaFVars argsEq  finalEq
  trace[bv_ack] "made ackermann equation: {ackEqn}"
  let (_ackEqn, g) ‚Üê g.note (Name.mkSimple s!"ackEqn{fn.f}") ackEqn
  return g

/--
For every bitvector (x : BitVec w), for every function `(f : BitVec w ‚Üí BitVec w')`,
walk every function application (f x), and add a new fvar (fx : BitVec w').
- Keep an equality that says `fx = f x`.
- For function application of f, for each pair of bitvectors x, y,
  add a hypothesis that says `x = y => fx = fy, with proof.

FUTUREWORK: This can be extended to work with functions where only the output type is bitblastable,
so this will allow us to ackemannize equations such as `f : Nat ‚Üí BitVec w ‚Üí BitVec w`, with `(f 0) x` and `(f 0) y`
being correctly handled. That is, we can add the equation `x = y ‚Üí f 0 x = f 0 y`.
-/
def ack (g : MVarId) : AckM MVarId := do
  g.withContext do
    let (target', g) ‚Üê introAckForExpr g (‚Üê g.getType)
    let g ‚Üê g.replaceTargetDefEq target'

    let hyps ‚Üê g.getNondepPropHyps
    let mut g := g
    for hyp in hyps do
      g ‚Üê g.withContext do
        withTraceNode m!"üéØ hyp '{‚Üê hyp.getType}'" (collapsed := false) do
          let hypG ‚Üê introAckForExpr g (‚Üê hyp.getType)
          pure hypG.2

    for (fn, arg2call) in (‚Üê get).fn2args2call do
      let argCallsArr := arg2call.toArray
      for hi : i in [0:argCallsArr.size] do
        let (arg‚ÇÅ, call‚ÇÅ) := argCallsArr[i]
        for hj : j in [i+1:argCallsArr.size] do
          let (arg‚ÇÇ, call‚ÇÇ) := argCallsArr[j]
          if ‚Üê areArgListsTriviallyDifferent arg‚ÇÅ arg‚ÇÇ then
            continue
          g ‚Üê mkAckThm g fn arg‚ÇÅ arg‚ÇÇ call‚ÇÅ call‚ÇÇ
    trace[bv_ack] "{checkEmoji} ack.{indentD g}"
    return g

end AckM

/-- Entry point for programmatic usage of `bv_ackermannize`. -/
def ackTac : TacticM Unit := do
  withoutRecover do
    liftMetaTactic fun g => do
      let g ‚Üê (AckM.ack g).run
      return [g]

end Ack

@[builtin_tactic Lean.Parser.Tactic.bvAckEager]
def evalBvAckEager : Tactic := fun
  | `(tactic| bv_ack_eager) =>
    Ack.ackTac
  | _ => throwUnsupportedSyntax

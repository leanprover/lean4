-- This is a preliminary list of aspirational goals for the new `auto` tactic.
-- I'm still trying to get a sense of the planned scope;
-- some of these may be quickly ruled out of scope!

macro "auto" : tactic => `(tactic| sorry)

namespace Nat

-- Nat.lt_asymm
example {a b : Nat} (h : a < b) : Â¬ b < a :=
  Nat.not_lt.2 (Nat.le_of_lt h)
  -- If `Nat.not_lt : Â¬a < b â†” b â‰¤ a` is a `simp` rule,
  -- and `Nat.le_of_lt : a < b â†’ a â‰¤ b` is a `have` rule:
  -- by auto

-- Nat.lt_iff_le_not_le
example {m n : Nat} : m < n â†” m â‰¤ n âˆ§ Â¬ n â‰¤ m :=
  âŸ¨fun h => âŸ¨Nat.le_of_lt h, Nat.not_le_of_gt hâŸ©, fun âŸ¨_, hâŸ© => Nat.lt_of_not_ge hâŸ©
  -- Is proving `â†”` via the constructor in scope? I presume so?
  -- If `Nat.not_le_of_gt : a < b â†’ Â¬b â‰¤ a` is an `apply` rule,
  -- and `Nat.not_le` is a `simp` rule:
  -- by auto

-- Nat.ne_iff_lt_or_gt
example {a b : Nat} : a â‰  b â†” a < b âˆ¨ b < a :=
  âŸ¨Nat.lt_or_gt_of_ne, fun | .inl h => Nat.ne_of_lt h | .inr h => Nat.ne_of_gt hâŸ©
  -- `Nat.lt_or_gt_of_ne : a â‰  b â†’ a < b âˆ¨ b < a` would be a `have` rule?
  -- We'll do cases on `Or`
  -- `Nat.ne_of_lt` and `Nat.ne_of_gt` would be `have` rules?

-- A simple logic puzzle: extract a witness from `hâ‚‚`, specialize `hâ‚` at it.
example (b : List Î±) (p : Î± â†’ Prop) (hâ‚ : âˆ€ a âˆˆ b, p a) (hâ‚‚ : âˆƒ a âˆˆ b, Â¬p a) : False :=
  by auto

-- From `Nat.testBit_two_pow_sub_succ`
example (h : x < 2 ^ (n + 1)) :
    decide ((2 ^ (n + 1) - (x + 1)) % 2 = 1) =
      (decide (0 < n + 1) && !decide (x % 2 = 1)) := by
  -- "just logic and omega":
  simp only [zero_lt_succ, decide_True, Bool.true_and]
  rw [â† decide_not, decide_eq_decide]
  omega

-- From `Nat.ne_zero_implies_bit_true`
example {x : Nat}
    {hyp : x > 0 â†’ x / 2 â‰  0 â†’ âˆƒ i, testBit (x / 2) i = true}
    {xnz : 2 * (x / 2) â‰  0}
    {x_pos : x > 0} : âˆƒ i, testBit x i = true := by
  -- Is this in scope? Much harder. One has to:
  -- * Realise that in `hyp` we could replace `testBit (x / 2) i` with `testBit x (i + 1)`.
  --   (this is the simp lemma `testBit_succ` in the opposite direction)
  -- * Now see that `hyp` could be used to instantiate the existential with `i + 1`.
  -- * After that, deduce `x / 2 â‰  0` from `xnz`.
  simp only [ne_eq, Nat.mul_eq_zero, Nat.add_zero, false_or] at xnz
  have âŸ¨d, difâŸ© := hyp x_pos xnz
  apply Exists.intro (d+1)
  simp_all only [gt_iff_lt, ne_eq, not_false_eq_true, forall_const, testBit_succ]

end Nat

namespace List

-- From `List.mem_filter`
example : x âˆˆ filter p as â†” x âˆˆ as âˆ§ p x := by
  -- Is it even in scope to try induction?
  induction as with
  | nil => simp
  | cons a as ih =>
    /-
    The original proof proceeds here as:
    ```
    by_cases h : p a <;> simp [*, or_and_right]
    Â· exact or_congr_left (and_iff_left_of_imp fun | rfl => h).symm
    Â· exact (or_iff_right fun âŸ¨rfl, h'âŸ© => h h').symm
    ```
    However it is not reasonable to get that one should use `by_cases p a` yet.
    -/
    -- It might be reasonable for `auto` to be aware of `filter_cons`,
    -- even though it is not a simp lemma.
    simp [filter_cons]
    -- Now it is reasonable to split:
    split
    Â· simp [*]
      sorry -- just logic from here
    Â· simp [*]
      sorry -- just slightly trickier logic from here


-- Recall:
-- theorem append_inj :
--     âˆ€ {sâ‚ sâ‚‚ tâ‚ tâ‚‚ : List Î±}, sâ‚ ++ tâ‚ = sâ‚‚ ++ tâ‚‚ â†’ length sâ‚ = length sâ‚‚ â†’ sâ‚ = sâ‚‚ âˆ§ tâ‚ = tâ‚‚

-- From `List.append_inj'`
example (h : sâ‚ ++ tâ‚ = sâ‚‚ ++ tâ‚‚) (hl : length tâ‚ = length tâ‚‚) : sâ‚ = sâ‚‚ âˆ§ tâ‚ = tâ‚‚ := by
  -- It seems unreasonable for `append_inj` to be a global `apply` rule,
  -- but it could be added local, or might be reasonable as a `have` rule.
  -- In either case, after using it,
  -- `auto` would need to deduce `sâ‚.length = sâ‚‚.length` from `hl`.
  -- If `auto` can apply `List.length` to `h`, then after simplifying this is just arithmetic.
  auto
  -- Original proof:
  -- append_inj h <| @Nat.add_right_cancel _ (length tâ‚) _ <| by
  -- let hap := congrArg length h; simp only [length_append, â† hl] at hap; exact hap

end List

namespace CategoryTheory

universe v u

macro "cat_tac" : tactic => `(tactic| (intros; (try ext); simp))

class Category (obj : Type u) : Type max u (v + 1) where
  Hom : obj â†’ obj â†’ Type v
  /-- The identity morphism on an object. -/
  id : âˆ€ X : obj, Hom X X
  /-- Composition of morphisms in a category, written `f â‰« g`. -/
  comp : âˆ€ {X Y Z : obj}, (Hom X Y) â†’ (Hom Y Z) â†’ (Hom X Z)
  /-- Identity morphisms are left identities for composition. -/
  id_comp : âˆ€ {X Y : obj} (f : Hom X Y), comp (id X) f = f := by cat_tac
  /-- Identity morphisms are right identities for composition. -/
  comp_id : âˆ€ {X Y : obj} (f : Hom X Y), comp f (id Y) = f := by cat_tac
  /-- Composition in a category is associative. -/
  assoc : âˆ€ {W X Y Z : obj} (f : Hom W X) (g : Hom X Y) (h : Hom Y Z), comp (comp f g) h = comp f (comp g h) := by cat_tac

attribute [simp] Category.id_comp Category.comp_id Category.assoc

infixr:10 " âŸ¶ " => Category.Hom
scoped notation "ğŸ™" => Category.id  -- type as \b1
scoped infixr:80 " â‰« " => Category.comp

structure Functor (C : Type uâ‚) [Category.{vâ‚} C] (D : Type uâ‚‚) [Category.{vâ‚‚} D] : Type max vâ‚ vâ‚‚ uâ‚ uâ‚‚ where
  /-- The action of a functor on objects. -/
  obj : C â†’ D
  /-- The action of a functor on morphisms. -/
  map : âˆ€ {X Y : C}, (X âŸ¶ Y) â†’ ((obj X) âŸ¶ (obj Y))
  /-- A functor preserves identity morphisms. -/
  map_id : âˆ€ X : C, map (ğŸ™ X) = ğŸ™ (obj X) := by cat_tac
  /-- A functor preserves composition. -/
  map_comp : âˆ€ {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z), map (f â‰« g) = (map f) â‰« (map g) := by cat_tac

attribute [simp] Functor.map_id Functor.map_comp

@[ext]
structure NatTrans [Category.{vâ‚, uâ‚} C] [Category.{vâ‚‚, uâ‚‚} D] (F G : Functor C D) : Type max uâ‚ vâ‚‚ where
  /-- The component of a natural transformation. -/
  app : âˆ€ X : C, F.obj X âŸ¶ G.obj X
  /-- The naturality square for a given morphism. -/
  naturality : âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), F.map f â‰« app Y = app X â‰« G.map f := by cat_tac

attribute [simp] NatTrans.naturality

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D] {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E]
variable {F G : Functor C D}

namespace NatTrans

protected def id (F : Functor C D) : NatTrans F F where app X := ğŸ™ (F.obj X)

@[simp] theorem id_app : (NatTrans.id F).app X = ğŸ™ (F.obj X) := rfl

protected def vcomp (Î± : NatTrans F G) (Î² : NatTrans G H) : NatTrans F H where
  app X := Î±.app X â‰« Î².app X
  naturality := by
    intros X Y f
    dsimp
    -- This is the first place where `cat_tac` lets us down, and ematching should save us.
    -- We can't rewrite by `Î±.naturality f` immediately, because the composition is associated incorrectly.
    rw [â† Category.assoc]
    rw [Î±.naturality f]
    rw [Category.assoc]
    rw [Î².naturality f]
    rw [â† Category.assoc]
    -- (Mathlib gets around this with a `@[reassoc]` attribute,
    -- that automatically generates copies of lemmas that fold in associativity.
    -- It can only ever get you "one step", however.)
    -- (Note that the ematching in Lean 3 couldn't quite do this one:
    -- just because there was a bug when we have two typeclass instances with different parameters,
    -- e.g. the two category instances here.)

@[simp] theorem vcomp_app (Î± : NatTrans F G) (Î² : NatTrans G H) (X : C) :
    (Î±.vcomp Î²).app X = Î±.app X â‰« Î².app X := rfl

end NatTrans

instance Functor.category : Category.{max uâ‚ vâ‚‚} (Functor C D) where
  Hom F G := NatTrans F G
  id F := NatTrans.id F
  comp Î± Î² := NatTrans.vcomp Î± Î²
  -- Here we're okay: all the proofs are handled by `cat_tac`.

@[simp]
theorem id_app (F : Functor C D) (X : C) : (ğŸ™ F : F âŸ¶ F).app X = ğŸ™ (F.obj X) := rfl

@[simp]
theorem comp_app {F G H : Functor C D} (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) (X : C) :
    (Î± â‰« Î²).app X = Î±.app X â‰« Î².app X := rfl

theorem app_naturality {F G : Functor C (Functor D E)} (T : F âŸ¶ G) (X : C) {Y Z : D} (f : Y âŸ¶ Z) :
    (F.obj X).map f â‰« (T.app X).app Z = (T.app X).app Y â‰« (G.obj X).map f := by
  cat_tac

theorem naturality_app {F G : Functor C (Functor D E)} (T : F âŸ¶ G) (Z : D) {X Y : C} (f : X âŸ¶ Y) :
    (F.map f).app Z â‰« (T.app Y).app Z = (T.app X).app Z â‰« (G.map f).app Z := by
  -- `simp` can't get us there, as one has to go uphill first.
  rw [â† comp_app]
  rw [T.naturality f]
  rw [comp_app]


end CategoryTheory
